PROGRAM SOSMAIN
USE RTUTILITY, ONLY : PI,FACTOR,SCL,IPSS_VIEWANGLE_GROUND, &
		PSEUDO_SPHERICAL_SHELL,SPHERICAL_SHELL_SINGLESCATTERING_CORRECTION,&
		I_SURFACE_ROUGHNESS_PARA,SIGMASQ,SHDWFLG,ENGFACNORM
USE PHASE_MATRIX_TRUNCATION, ONLY : NUMMIEANGMAX
USE SnowBRDF
USE IRRADSUNL

IMPLICIT NONE

INTEGER :: NTHETAIN,NTHETAOUT,NPHIOUT,NMIEHEADER,NDET
REAL*8,DIMENSION(:),ALLOCATABLE::THETA_IN,MU_IN

CHARACTER(LEN=180),DIMENSION(:),ALLOCATABLE :: FMIEINPUT
CHARACTER(LEN=180) :: FMIE_PMTX,STRTMP

! DATA TRANSFER TO RTSOS CALLABLE
INTEGER :: NREC,NCOLINPUT,NQUADAINPUT,NQUADOINPUT, &
           NUMMIERT,NUMMIEUSE,MAXLORDINPUT,MAXMORDINPUT
REAL*8 :: DELTATAUAINPUT,DELTATAUOINPUT
REAL*8,DIMENSION(:,:),ALLOCATABLE :: RECDATASTREAM
REAL*8,DIMENSION(:,:,:),ALLOCATABLE :: PHMXDATASTREAM !,COEFFDATASTREAM
INTEGER,DIMENSION(:),ALLOCATABLE :: NUMMIEANGINPUT

INTEGER :: ITHETA0,IREAD,IMIE,IOFLG

REAL*8,DIMENSION(:),ALLOCATABLE :: PHIOUT,MUOUT,THETAOUT
REAL*8 :: NMBREINPUT,NMBIMINPUT
LOGICAL :: WLR_FLAG_INPUT,WCFLG_INPUT,OCEAN_INPUT,fSnowBRDF_INPUT,fRossLiBRDF_INPUT
INTEGER :: DELTAM_INPUT
! DELTAM = 0 USE DELTA M FIT FOR THE PHASE MATRIX
!          1 USE DELTA M + FIT
!          2 USE DELTA FIT

REAL*8,DIMENSION(:,:) ,ALLOCATABLE:: DIRAD  ! DIRAD(:,1) DOWNWELLING IRADIANCE
                                            ! DIRAD(:,2) UPWELLING IRADIANCE
                                            ! First dimention is detector label
REAL*8,DIMENSION(:,:,:,:),ALLOCATABLE :: DSTOKES
! DSTOKES(NWV,       NTHETAOUT,       NPHIOUT,         4)
! ITH WAVELENGTH , theta out index, phi out index, stokes parameters I Q U V

REAL*8,DIMENSION(:,:,:) ,ALLOCATABLE:: DIRADFULL  ! DIRAD(:,1) DOWNWELLING IRADIANCE
                                            ! DIRAD(:,2) UPWELLING IRADIANCE
                                            ! First dimention is detector label
REAL*8,DIMENSION(:,:,:,:,:),ALLOCATABLE :: DSTOKESFULL
! DSTOKES(NWV,       NTHETAOUT,       NPHIOUT,         4)
! ITH WAVELENGTH , theta out index, phi out index, stokes parameters I Q U V


REAL*8 :: RTMP,RTMP1,RTMP2,RTMP3,RTMP4,RTMP5

integer time_array_0(8)
real start_time, finish_time

INTEGER :: NARGS,IARGC
CHARACTER(LEN=180) :: INFILE   ! Modification for scripting (MJG)
CHARACTER(LEN=180) :: OUTFILE  ! Modification for scripting (MJG)

CHARACTER*360 ::aux_dir

! MODIFICATIONS TO ALLOW SCRIPTING (MJG)

! Read inputs from the command line
NARGS = IARGC()
IF(NARGS.LT.2) THEN
WRITE(*,*) ''
WRITE(*,*) '***ERROR***'
WRITE(*,*) 'TOO FEW INPUTS'
WRITE(*,*) '***ERROR***'
WRITE(*,*) ''
WRITE(*,*) 'USEAGE:'
WRITE(*,*) 'rtsos_rao_dg.exe input.file output.file'
WRITE(*,*) ''
STOP
ENDIF

!  First argument is the input file
CALL GETARG(1,INFILE)

!  Second argument is the output file

CALL GETARG(2,OUTFILE)

! READING SOLAR AND VIEWING ANGLE INFORMATION
OPEN(unit=3,file='sosi.amu',status='old')

READ(3,*)NTHETAIN
ALLOCATE(THETA_IN(NTHETAIN),MU_IN(NTHETAIN))

DO IREAD=1,NTHETAIN
   READ(3,*)THETA_IN(IREAD)
   THETA_IN(IREAD)=THETA_IN(IREAD)*FACTOR
   MU_IN(IREAD)=COS(THETA_IN(IREAD))
END DO

READ(3,*)NTHETAOUT,NPHIOUT
ALLOCATE(THETAOUT(NTHETAOUT),MUOUT(NTHETAOUT),PHIOUT(NPHIOUT))

DO IREAD=1,NTHETAOUT
  READ(3,*)THETAOUT(IREAD)
  MUOUT(IREAD)=COS(THETAOUT(IREAD)*FACTOR)
ENDDO

DO IREAD=1,NPHIOUT
  READ(3,*)PHIOUT(IREAD)
  PHIOUT(IREAD)=PHIOUT(IREAD)*FACTOR
ENDDO

CLOSE(3)
NMBREINPUT=0.0D0
NMBIMINPUT=0.0D0
open(unit=1,file=INFILE,status='old')
IREAD=0
NDET=0
NREC=0

LOOP_READ_CONFIG : DO WHILE (IREAD>-2000)
  READ(1,*)IREAD,RTMP,RTMP1,RTMP2,RTMP3,RTMP4,RTMP5
  NREC=NREC+1
  IF(IREAD==-1000)NDET=NDET+1
  IF(IREAD==-200) THEN
     IF(RTMP1<1.0D0)THEN
       NMBREINPUT=RTMP2
       NMBIMINPUT=RTMP3
     ENDIF
     EXIT LOOP_READ_CONFIG
  ENDIF
  IF(IREAD==-201) THEN
     NMBREINPUT=RTMP4
     NMBIMINPUT=RTMP5
     EXIT LOOP_READ_CONFIG
  ENDIF

  IF(IREAD==-202) THEN
	   fSnowBRDF_INPUT=.TRUE.
	   EXIT LOOP_READ_CONFIG
  ENDIF

  IF(IREAD==-203) THEN
       fRossLiBRDF_INPUT=.TRUE.
	   EXIT LOOP_READ_CONFIG
  ENDIF


  IF(IREAD==-101 .or. IREAD==-102) EXIT LOOP_READ_CONFIG
ENDDO LOOP_READ_CONFIG
REWIND 1

ALLOCATE(RECDATASTREAM(NREC,7))
OCEAN_INPUT=.FALSE.
DO IREAD=1,NREC
  READ(1,*)RECDATASTREAM(IREAD,1:7)
  IF(ABS(RECDATASTREAM(IREAD,1)+100)<1.0E-6)OCEAN_INPUT=.TRUE.
  IF(OCEAN_INPUT .AND. RECDATASTREAM(IREAD,6)>0) &
     STOP 'INELASTIC CALCULATION CAN ONLY BE MADE WITH A SPECTIAL MAIN PROGRAM'
!  WRITE(*,*)RECDATASTREAM(IREAD,1),RECDATASTREAM(IREAD,2),&
!            RECDATASTREAM(IREAD,3),RECDATASTREAM(IREAD,4)
ENDDO
READ(1,*) NCOLINPUT,NQUADAINPUT,NQUADOINPUT 
READ(1,*) NUMMIEUSE,MAXLORDINPUT,MAXMORDINPUT
ALLOCATE(FMIEINPUT(NUMMIEUSE))
DO IMIE=1,NUMMIEUSE
  READ(1,*)FMIEINPUT(IMIE)
ENDDO
IF(fSnowBRDF_INPUT) THEN
    READ(1,'(A)')snow_brdf_path
	IF(index(snow_brdf_path,'#')>1) &
	   snow_brdf_path=trim(snow_brdf_path(1:index(snow_brdf_path,'#')-1))
ENDIF
CLOSE(1)
ALLOCATE(NUMMIEANGINPUT(NUMMIEUSE),PHMXDATASTREAM(NUMMIEUSE,NUMMIEANGMAX,0:6))
PHMXDATASTREAM=0.0D0
DO IMIE=1,NUMMIEUSE
  FMIE_PMTX=FMIEINPUT(IMIE)
  OPEN(unit=1,file=trim(FMIE_PMTX),status='OLD')
      NMIEHEADER=0
    IREAD=0
    IOFLG=0
    LOOP_READ_MIEPHMX: DO WHILE(IOFLG==0)
       READ(1,*,IOSTAT=IOFLG)STRTMP 
       IREAD=IREAD+1
       STRTMP=TRIM(STRTMP)
       IF(SCAN(STRTMP(1:1),'<')>0 .and. SCAN(STRTMP(2:2),' ')>0)NMIEHEADER=IREAD
    ENDDO LOOP_READ_MIEPHMX
    NUMMIEANGINPUT(IMIE)=IREAD-NMIEHEADER
    IF(NUMMIEANGINPUT(IMIE)>NUMMIEANGMAX) STOP 'INCREASE NUMMIEANGMAX'
    REWIND(1)
    DO IREAD=1,NMIEHEADER
       READ(1,*) 
    ENDDO
    DO IREAD=1,NUMMIEANGINPUT(IMIE)
       READ(1,*)PHMXDATASTREAM(IMIE,IREAD,0:6)
       IF(ABS(PHMXDATASTREAM(IMIE,IREAD,0)-180.0D0)<1.0E-4) EXIT
    ENDDO
    NUMMIEANGINPUT(IMIE)=IREAD
    CLOSE(1)
!write(*,*)'imie=',imie
!DO IREAD=1,NUMMIEANGINPUT(IMIE)
!  write(*,*)PHMXDATASTREAM(IMIE,IREAD,0:6)
!ENDDO

ENDDO

ALLOCATE(DIRAD(NDET,2),DSTOKES(NDET,NTHETAOUT,NPHIOUT,4))
ALLOCATE(DIRADFULL(NDET,NTHETAIN,2),DSTOKESFULL(NDET,NTHETAIN,NTHETAOUT,NPHIOUT,4))

DELTATAUAINPUT=0.01D0        ! ADJUST FOR ACCURATE OPTICAL INTEGRATION
DELTATAUOINPUT=0.01D0
!SHDWFLG=.false.
WLR_FLAG_INPUT= .false.
WCFLG_INPUT = .false.
!ENGFACNORM=.true.
DELTAM_INPUT = 1
NUMMIERT=NUMMIEUSE

!SCL = .true.
!SPHERICAL_SHELL_SINGLESCATTERING_CORRECTION=.true.
!PSEUDO_SPHERICAL_SHELL=.true.
!IPSS_VIEWANGLE_GROUND=1

I_SURFACE_ROUGHNESS_PARA=1

call date_and_time(values=time_array_0)
start_time = time_array_0 (5) * 3600 + time_array_0 (6) * 60 &
           + time_array_0 (7) + 0.001 * time_array_0 (8)

aux_dir='00000'
CALL SUNLREADIN(aux_dir)

DO ITHETA0=1,NTHETAIN

   CALL RTSOS(NREC,NDET,RECDATASTREAM,NCOLINPUT,NQUADAINPUT,NQUADOINPUT, &
     DELTATAUAINPUT,DELTATAUOINPUT,NUMMIERT,MAXLORDINPUT,MAXMORDINPUT,    &
     NUMMIEANGINPUT,PHMXDATASTREAM,MU_IN(ITHETA0),NTHETAOUT,&
     NPHIOUT,PHIOUT,MUOUT,DIRAD,DSTOKES,WLR_FLAG_INPUT,WCFLG_INPUT,DELTAM_INPUT)

!    CALL RT_SINGLE_SCATTERING(NREC,NDET,RECDATASTREAM,DELTATAUAINPUT,&
!        DELTATAUOINPUT,NUMMIEUSE,NUMMIEANGINPUT,MAXLORDINPUT,       &
!        PHMXDATASTREAM,MU_IN,NTHETAOUT,NPHIOUT,PHIOUT,MUOUT,DIRAD,    &
!        DSTOKES,WCFLG_INPUT,DELTAM_INPUT)

  DIRADFULL(1:NDET,ITHETA0,1:2)=DIRAD(1:NDET,1:2)
  DSTOKESFULL(1:NDET,ITHETA0,1:NTHETAOUT,1:NPHIOUT,1:4)=&
         DSTOKES(1:NDET,1:NTHETAOUT,1:NPHIOUT,1:4)
ENDDO
call date_and_time(values=time_array_0)
finish_time = time_array_0 (5) * 3600 + time_array_0 (6) * 60 &
           + time_array_0 (7) + 0.001 * time_array_0 (8)
write(*,*)'rtsos elapsed time =', finish_time-start_time

CALL MAINSTOKESOUT(NREC,NDET,RECDATASTREAM,NCOLINPUT,NQUADAINPUT,NQUADOINPUT, &
     DELTATAUAINPUT,DELTATAUOINPUT,NUMMIEUSE,MAXLORDINPUT,MAXMORDINPUT,    &
     NTHETAIN,MU_IN,NTHETAOUT,NPHIOUT,PHIOUT,MUOUT,&
     DIRADFULL,DSTOKESFULL,WLR_FLAG_INPUT,WCFLG_INPUT,DELTAM_INPUT,FMIEINPUT,&
     OUTFILE,SIGMASQ)

CLOSE(55)
DEALLOCATE(DIRAD,DSTOKES,DIRADFULL,DSTOKESFULL,MU_IN,THETA_IN)
DEALLOCATE(PHIOUT,MUOUT,RECDATASTREAM,PHMXDATASTREAM,FMIEINPUT,NUMMIEANGINPUT)
CALL DEALL_SUNL

ENDPROGRAM SOSMAIN

SUBROUTINE  MAINSTOKESOUT(NREC,NDET,RECDATASTREAM,NCOLINPUT,NQUADAINPUT,NQUADOINPUT, &
     DELTATAUAINPUT,DELTATAUOINPUT,NUMMIEUSE,MAXLORDINPUT,MAXMORDINPUT,    &
     NTHETAIN,MU_IN,NTHETAOUT,NPHIOUT,PHIOUT,MUOUT,&
     DIRADFULL,DSTOKESFULL,WLR_FLAG_INPUT,WCFLG_INPUT,DELTAM_INPUT,FMIEINPUT,&
     OUTFILE,SIGMASQ)

IMPLICIT none

INTEGER :: NREC,NDET,NCOLINPUT,NQUADAINPUT,NQUADOINPUT,NTHETAOUT,&
           MAXLORDINPUT,MAXMORDINPUT,NTHETAIN,NPHIOUT,NUMMIEUSE,ICOM
REAL*8 ::DELTATAUAINPUT,DELTATAUOINPUT,SIGMASQ
LOGICAL :: WLR_FLAG_INPUT,WCFLG_INPUT
INTEGER :: DELTAM_INPUT

REAL*8,DIMENSION(NREC,7) :: RECDATASTREAM
REAL*8,DIMENSION(NTHETAIN) ::MU_IN
REAL*8,DIMENSION(NTHETAOUT) :: MUOUT
REAL*8,DIMENSION(NPHIOUT) :: PHIOUT

REAL*8,DIMENSION(NDET,NTHETAIN,2) :: DIRADFULL  ! DIRADFULL(:,1) DOWNWELLING IRADIANCE
                                   ! DIRAD(:,2) UPWELLING IRADIANCE
REAL*8,DIMENSION(NDET,NTHETAIN,NTHETAOUT,NPHIOUT,4) :: DSTOKESFULL
CHARACTER(LEN=180),DIMENSION(NUMMIEUSE) :: FMIEINPUT

LOGICAL :: MUFLG

REAL*8,PARAMETER :: PI=3.141592653589793d0
REAL*8,PARAMETER :: FACTOR=PI/180.0d0
INTEGER:: ITHETAIN,ITHETAOUT,IPHIOUT,IDET,IMIE,IREC

CHARACTER(LEN=180) :: OUTFILE  ! Modification for scripting (MJG)

REAL*8 :: RTMP

MUFLG=.FALSE.

OPEN(UNIT=11,FILE=OUTFILE,STATUS='REPLACE')

WRITE(11,'("VECTOR RADIATIVE TRANSFER RESULTS BASED ON THE SOS METHOD")')
WRITE(11,'("OPTICAL DEPTH INTEGRATION STEPS=",2(2X,E11.4))')DELTATAUAINPUT,DELTATAUOINPUT
WRITE(11,'("LAYER INDEX, IPT, TAUB, LBDOM, FLUORESCENCE STRENGTH")')
DO IREC=1,NREC
WRITE(11,'(7(E13.6,2X))')RECDATASTREAM(IREC,1:7)
ENDDO

DO IMIE=1,NUMMIEUSE
  WRITE(11,'("MIEFILE USED: ",A40)')FMIEINPUT(IMIE)
ENDDO

IF(WLR_FLAG_INPUT) THEN
  WRITE(11,'("WATER LEAVING RADIANCE WAS CALCULATED")') 
ELSE
  WRITE(11,'("TOTAL RADIANCE WAS CALCULATED")') 
ENDIF
WRITE(11,'("TOTAL IRRADIANCE WAS CALCULATED")') 
IF(WCFLG_INPUT) THEN
  WRITE(11,'("FOAM  (WHITE CAP) PARAMETERIZATION IS USED")') 
ELSE
  WRITE(11,'("NO FOAM (WHITE CAP) PARAMETERIZATION IS USED")') 
ENDIF
IF(DELTAM_INPUT==0) THEN
  WRITE(11,'("DELTAM IS USED")') 
ELSEIF(DELTAM_INPUT==1) THEN
  WRITE(11,'("DLETAM PLUS IS USED")')
ELSE
  WRITE(11,'("DELTAM FIT IS USED")')
ENDIF
WRITE(11,'("NCOL,NQUADAINPUT,NQUADOINPUT,MAXLORD,MAXMORD USED=",5(I4,2X))') &
      NCOLINPUT,NQUADAINPUT,NQUADOINPUT,MAXLORDINPUT,MAXMORDINPUT
WRITE(11,'("SigmaSQ=",(E13.6))') SIGMASQ

DO IDET=1,NDET
WRITE(11,'("DETECTOR",2x,I4)')IDET
DO ITHETAIN=1,NTHETAIN
    WRITE(11,'("SOLAR MU0=",2x,E13.6)')MU_IN(ITHETAIN)
    WRITE(11,'("DOWNWELLING IRRADIANCE=",E13.6)')DIRADFULL(IDET,ITHETAIN,1)
    WRITE(11,'("UPWELLING IRRADIANCE=",E13.6)')DIRADFULL(IDET,ITHETAIN,2)
    IF(MUFLG)THEN
       WRITE(11,'("COS(THETA)",1x,"PHI", 7x,"Radiance", &
            &7x,  "Q", 16x ,"U", 15x, "V")')
    ELSE
      WRITE(11,'(2x,"THETA",4x,"PHI", 7x,"Radiance", &
            &9x,  "Q", 18x ,"U", 17x, "V")')
    ENDIF

    DO IPHIOUT=1,NPHIOUT
    DO ITHETAOUT=1,NTHETAOUT
      IF(MUFLG)THEN
        RTMP=MUOUT(ITHETAOUT)
      ELSE
        RTMP=ACOS(MUOUT(ITHETAOUT))/PI*180.0D0
      ENDIF
      DO ICOM=1,4
        IF(abs(DSTOKESFULL(IDET,ITHETAIN,ITHETAOUT,IPHIOUT,ICOM))<1.0E-32)&
           DSTOKESFULL(IDET,ITHETAIN,ITHETAOUT,IPHIOUT,ICOM)=0.0D0
      ENDDO
      WRITE(11,"(1X,F6.2,2X,F6.2,4(2X,ES16.6))") &
         RTMP,PHIOUT(IPHIOUT)*180.0d0/PI,DSTOKESFULL(IDET,ITHETAIN,ITHETAOUT,IPHIOUT,1:4) 
 
    ENDDO
    ENDDO
ENDDO
ENDDO

close(11)
RETURN

END SUBROUTINE  MAINSTOKESOUT
