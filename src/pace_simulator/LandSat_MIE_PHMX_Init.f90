MODULE MIE_PHMX_LandSat
USE LandSat_INSTRUMENT_DESIGN,ONLY : NWV_BAND,WV_BAND
USE AEROSOL_MICROPHYSICAL_MODEL
USE RTUTILITY, ONLY : PI,FACTOR,NUMMIEANGMAX
USE Mie_Scat_Montes
INTEGER,DIMENSION(3) :: NUM_SCAT_ANG
REAL*8 :: REFF_MINERAL,AREA_MINERAL,WAVELENGTH_MICRON_REF
REAL*8,DIMENSION(NWV_BAND+1):: CEXT_ARSL_BAND,CSCAT_ARSL_BAND, &
                 CEXT_Plankton_BAND,CSCAT_Plankton_BAND, &
                 CEXT_Mineral_BAND,CSCAT_Mineral_BAND
REAL*8,DIMENSION(NWV_BAND)::BBFRAC_Plankton_BAND,BBFRAC_Mineral_BAND

REAL*8,DIMENSION(NWV_BAND,NUMMIEANGMAX,0:6)::PHMXMIE_ARSL_LandSat
REAL*8,DIMENSION(NWV_BAND,NUMMIEANGMAX,0:6)::PHMXMIE_Plankton_LandSat
REAL*8,DIMENSION(NWV_BAND,NUMMIEANGMAX,0:6)::PHMXMIE_Mineral_LandSat
CONTAINS

SUBROUTINE PHMXMIE_LandSat_INIT(FREEFORMFLAG,IAEROSOL,IRH,REFF1,VEFF1,REFF2,VEFF2,&
		WV_LandSat_REF,MRR1,MRI1,MRR2,MRI2,ARSLND1,ARSLND2,PHYTOPLANKTON_INDEX_REFRACTION,&
            PHYTOPLANKTON_SPECTRAL_SLOPE,SEDIMENT_INDEX_REFRACTION,SEDIMENT_SPECTRAL_SLOPE)
IMPLICIT NONE

INTEGER,INTENT(IN) :: FREEFORMFLAG
! FREEFORMFLAG==0  FREE FORM AEROSOL CALCULATION, USING REFF, VEFF, MR,MI AS INPUTS
! FREEFORMFLAG==1  PRE-BUILD AEROSOL CALCULATION
! FREEFORMFLAG==2  PLANKTON MIE CALCULATION 
! FREEFORMFLAG==3  MINERAL MIE CALCULATION

INTEGER,INTENT(IN) :: IAEROSOL,IRH
REAL*8,INTENT(IN)::WV_LandSat_REF
REAL*8,INTENT(INOUT)::REFF1,VEFF1,REFF2,VEFF2,ARSLND1,ARSLND2
REAL*8,DIMENSION(NWV_BAND+1),INTENT(INOUT)::MRR1,MRI1,MRR2,MRI2

REAL*8,DIMENSION(NWV_BAND)::WV_BAND_MICRON
REAL*8,DIMENSION(NUMMIEANGMAX,0:6)::PHMXMIE_LOCAL1,PHMXMIE_LOCAL2

REAL*8, INTENT(IN) :: PHYTOPLANKTON_INDEX_REFRACTION,PHYTOPLANKTON_SPECTRAL_SLOPE,&
                      SEDIMENT_INDEX_REFRACTION,SEDIMENT_SPECTRAL_SLOPE

INTEGER :: NDISTR,NANGMIE_LOCAL,ICOM
REAL*8 :: AA1,BB1,AA2,BB2,GAMMAI,REFFO,VEFFO,AREA,CEXT1,CEXT2,CSCAT1,CSCAT2

REAL*8::REFF1_LOCAL,VEFF1_LOCAL,REFF2_LOCAL,VEFF2_LOCAL,ARSLND1_LOCAL,ARSLND2_LOCAL

REAL*8 :: RTMP,RTMP1,BBFRAC_LOCAL,WAVELENGTH_MICRON,NR_TEMP,NI_TEMP,&
          FINE_MODE_FRACTION_VOLUME,WAVELENGTH_NANO

INTEGER :: IWV_BAND,ITYPE

WAVELENGTH_MICRON_REF=WV_LandSat_REF/1000.0d0
CALL WV_REF_ASSIGN(WAVELENGTH_MICRON_REF) ! aerosol optical depth reference wavelength

WV_BAND_MICRON=WV_BAND/1000.0D0

IF(FREEFORMFLAG==0)THEN
  IF(REFF1<0 .OR. REFF2<0 .OR. VEFF1<0 .OR. VEFF2<0 .OR. ARSLND1<0 .OR. ARSLND2<0) &
       STOP 'CHECK AEROSOL MICROPHYSICAL INPUT PARAMETERS'
  DO IWV_BAND=1,NWV_BAND
     IF(MRR1(IWV_BAND)<0.0) STOP 'CHECK AEROSOL REFRACTIVE INDEX'
     IF(MRI1(IWV_BAND)<0.0) STOP 'CHECK AEROSOL REFRACTIVE INDEX'
     IF(MRR2(IWV_BAND)<0.0) STOP 'CHECK AEROSOL REFRACTIVE INDEX'
     IF(MRI2(IWV_BAND)<0.0) STOP 'CHECK AEROSOL REFRACTIVE INDEX'
  ENDDO
  REFF1_LOCAL=REFF1
  VEFF1_LOCAL=VEFF1
  REFF2_LOCAL=REFF2
  VEFF2_LOCAL=VEFF2
  NDISTR=2
ELSEIF(FREEFORMFLAG==1)THEN

  IF(IAEROSOL<=20)CALL ARSL_INDX_INIT(NWV_BAND,WV_BAND_MICRON)

  IF(IAEROSOL==1)THEN
       REFF1=RTROPOSPHERE(IRH)
       REFF2=RRURALC(IRH)
  ELSEIF(IAEROSOL<=6)THEN
       REFF1=RURBANF(IRH)
       REFF2=RURBANC(IRH)
  ELSEIF(IAEROSOL<=9)THEN
       REFF1=RTROPOSPHERE(IRH)
       REFF2=ROCEANIC(IRH)
  ELSEIF(IAEROSOL==10)THEN
       REFF1=RTROPOSPHERE(IRH)
       REFF2=0.0D0
  ELSEIF(IAEROSOL>10 .and.IAEROSOL<=20)THEN
       REFF1=exp(log(RZIAF(IRH))-3.0*SIGF_ZIA*SIGF_ZIA)
            ! Rg in number space read Eq.3a in Ahmad 2000 for details
       REFF2=exp(log(RZIAC(IRH))-3.0*SIGC_ZIA*SIGC_ZIA)
  ELSEIF(IAEROSOL>20 .and.IAEROSOL<=30)THEN
       WRITE(*,*) 'IAEROSOL BETWEEN 20 AND 30 ARE NOT IMPLEMENTED IN THIS VERSION'
       stop 'IAEROSOL out of bound'
  ELSEIF(IAEROSOL>30 .and. IAEROSOL<=30+NTYPE)THEN
		REFF1=MM_rn_f(IAEROSOL-30)
		REFF2=MM_rn_c(IAEROSOL-30)
  ELSE
        WRITE(*,*) 'IAEROSOL=',IAEROSOL
        stop 'IAEROSOL out of bound'
  ENDIF
  IF(IAEROSOL<=10)THEN
     VEFF1=SIGF_SF
     VEFF2=SIGC_SF
  ELSEIF(IAEROSOL>10 .and.IAEROSOL<=20)THEN
     VEFF1=SIGF_ZIA
     VEFF2=SIGC_ZIA
  ELSEIF(IAEROSOL>30 .and. IAEROSOL<=30+NTYPE)THEN
	 VEFF1=MM_sigman_f(IAEROSOL-30)
	 VEFF2=MM_sigman_c(IAEROSOL-30)
  ENDIF

  IF(IAEROSOL<7)THEN
     ARSLND1=0.999875
     ARSLND2=0.000125
  ELSEIF(IAEROSOL==7)THEN
     ARSLND1=0.99
     ARSLND2=0.01
  ELSEIF(IAEROSOL==8)THEN
     ARSLND1=0.995
     ARSLND2=0.005
  ELSEIF(IAEROSOL==9)THEN
     ARSLND1=0.998
     ARSLND2=0.002
  ELSEIF(IAEROSOL==10)THEN
     ARSLND1=1.0D0
     ARSLND2=0.0D0
  ELSE
     IF(IAEROSOL<=20)THEN
         FINE_MODE_FRACTION_VOLUME=RATIO_FINE_MODE_ZIA(IAEROSOL-10)
	 ELSEIF(IAEROSOL>30)THEN
         FINE_MODE_FRACTION_VOLUME=MM_FCV_f(IAEROSOL-30)
     ENDIF
     RTMP=4.0D0*PI*(REFF1**3)/3.0D0
     RTMP=FINE_MODE_FRACTION_VOLUME/RTMP*EXP(-4.5D0*VEFF1*VEFF1)
     RTMP1=4.0D0*PI*(REFF2**3)/3.0D0
     RTMP1=(1.0D0-FINE_MODE_FRACTION_VOLUME)/RTMP1*EXP(-4.5D0*VEFF2*VEFF2)
     RTMP=RTMP/(RTMP+RTMP1)
     ARSLND1=RTMP
     ARSLND2=(1.0D0-RTMP)
!     WRITE(*,*)'FINE MODE FRACTION=',RTMP
  ENDIF

  IF(IAEROSOL==1)THEN !rural 
       MRR1(1:NWV_BAND+1)=MR_TRPOSPHERE(1:NWV_BAND+1,IRH)
       MRI1(1:NWV_BAND+1)=MI_TRPOSPHERE(1:NWV_BAND+1,IRH)
       MRR2(1:NWV_BAND+1)=MR_RURALC(1:NWV_BAND+1,IRH)
       MRI2(1:NWV_BAND+1)=MI_RURALC(1:NWV_BAND+1,IRH)
  ELSEIF(IAEROSOL==2)THEN ! URBAN
       MRR1(1:NWV_BAND+1)=MR_URBANF(1:NWV_BAND+1,IRH)
       MRI1(1:NWV_BAND+1)=MI_URBANF(1:NWV_BAND+1,IRH)
       MRR2(1:NWV_BAND+1)=MR_URBANC(1:NWV_BAND+1,IRH)
       MRI2(1:NWV_BAND+1)=MI_URBANC(1:NWV_BAND+1,IRH)
  ELSEIF(IAEROSOL==3)THEN ! URBAN A
       MRR1(1:NWV_BAND+1)=MR_URBANAF(1:NWV_BAND+1,IRH)
       MRI1(1:NWV_BAND+1)=MI_URBANAF(1:NWV_BAND+1,IRH)
       MRR2(1:NWV_BAND+1)=MR_URBANAC(1:NWV_BAND+1,IRH)
       MRI2(1:NWV_BAND+1)=MI_URBANAC(1:NWV_BAND+1,IRH)
  ELSEIF(IAEROSOL==4)THEN  ! URBAN A1
       MRR1(1:NWV_BAND+1)=MR_URBANA1F(1:NWV_BAND+1,IRH)
       MRI1(1:NWV_BAND+1)=MI_URBANA1F(1:NWV_BAND+1,IRH)
       MRR2(1:NWV_BAND+1)=MR_URBANA1C(1:NWV_BAND+1,IRH)
       MRI2(1:NWV_BAND+1)=MI_URBANA1C(1:NWV_BAND+1,IRH)
    ELSEIF(IAEROSOL==5)THEN! URBAN A2
       MRR1(1:NWV_BAND+1)=MR_URBANA2F(1:NWV_BAND+1,IRH)
       MRI1(1:NWV_BAND+1)=MI_URBANA2F(1:NWV_BAND+1,IRH)
       MRR2(1:NWV_BAND+1)=MR_URBANA2C(1:NWV_BAND+1,IRH)
       MRI2(1:NWV_BAND+1)=MI_URBANA2C(1:NWV_BAND+1,IRH)
    ELSEIF(IAEROSOL==6)THEN! URBAN A3
       MRR1(1:NWV_BAND+1)=MR_URBANA3F(1:NWV_BAND+1,IRH)
       MRI1(1:NWV_BAND+1)=MI_URBANA3F(1:NWV_BAND+1,IRH)
       MRR2(1:NWV_BAND+1)=MR_URBANA3C(1:NWV_BAND+1,IRH)
       MRI2(1:NWV_BAND+1)=MI_URBANA3C(1:NWV_BAND+1,IRH)
    ELSEIF(IAEROSOL>=7 .AND. IAEROSOL<=9)THEN! MARINETIME
       MRR1(1:NWV_BAND+1)=MR_TRPOSPHERE(1:NWV_BAND+1,IRH)
       MRI1(1:NWV_BAND+1)=MI_TRPOSPHERE(1:NWV_BAND+1,IRH)
       MRR2(1:NWV_BAND+1)=MR_OCEANIC(1:NWV_BAND+1,IRH)
       MRI2(1:NWV_BAND+1)=MI_OCEANIC(1:NWV_BAND+1,IRH)
    ELSEIF(IAEROSOL==10)THEN
       MRR1(1:NWV_BAND+1)=MR_TRPOSPHERE(1:NWV_BAND+1,IRH)
       MRI1(1:NWV_BAND+1)=MI_TRPOSPHERE(1:NWV_BAND+1,IRH)
       MRR2(1:NWV_BAND+1)=1.0
       MRI2(1:NWV_BAND+1)=0.0
    ELSEIF(IAEROSOL>10 .AND. IAEROSOL<=20)THEN
       MRR1(1:NWV_BAND+1)=MR_ZIAF(1:NWV_BAND+1,IRH)
       MRI1(1:NWV_BAND+1)=MI_ZIAF(1:NWV_BAND+1,IRH)
       MRR2(1:NWV_BAND+1)=MR_ZIAC(1:NWV_BAND+1,IRH)
       MRI2(1:NWV_BAND+1)=MI_ZIAC(1:NWV_BAND+1,IRH)
    ELSEIF(IAEROSOL>30)THEN
       ITYPE=IAEROSOL-30
       DO IWV_BAND=1,NWV_BAND+1
          IF(IWV_BAND>NWV_BAND)THEN
			WAVELENGTH_NANO=WV_LandSat_REF
          ELSE
            WAVELENGTH_NANO=WV_BAND(IWV_BAND)
          ENDIF
          CALL MM_REFRACTIVE_INDEX_INTERP(ITYPE,WAVELENGTH_NANO,NR_TEMP,NI_TEMP)
		  MRR1(IWV_BAND)=NR_TEMP
		  MRI1(IWV_BAND)=NI_TEMP
       ENDDO
       MRR2=MRR1
       MRI2=MRI1
    ENDIF
! CONVERT TO EFFECT RADIUS AND VARIANCE BEFORE CALL SPHER_INTERFACE
    REFF1_LOCAL=REFF1
    VEFF1_LOCAL=VEFF1
    REFF2_LOCAL=REFF2
    VEFF2_LOCAL=VEFF2

    VEFF1_LOCAL=VEFF1_LOCAL*VEFF1_LOCAL
    VEFF2_LOCAL=VEFF2_LOCAL*VEFF2_LOCAL

    REFF1_LOCAL=REFF1_LOCAL*EXP(2.5D0*VEFF1_LOCAL)
    REFF2_LOCAL=REFF2_LOCAL*EXP(2.5D0*VEFF2_LOCAL)
    VEFF1_LOCAL=EXP(VEFF1_LOCAL)-1.0D0
    VEFF2_LOCAL=EXP(VEFF2_LOCAL)-1.0D0

! pass effective radius and variance back to main program for output
	REFF1=REFF1_LOCAL
	VEFF1=VEFF1_LOCAL
	REFF2=REFF2_LOCAL
	VEFF2=VEFF2_LOCAL

    IF(IAEROSOL<=20) CALL ARSL_INDX_DEALLO
    NDISTR=2
ELSEIF(FREEFORMFLAG==2)THEN
    NDISTR=7
    MRR1=PHYTOPLANKTON_INDEX_REFRACTION
    MRI1=0.0D0
    GAMMAI=PHYTOPLANKTON_SPECTRAL_SLOPE
ELSEIF(FREEFORMFLAG==3)THEN
    NDISTR=7
    MRR1=SEDIMENT_INDEX_REFRACTION
    MRI1=0.0D0
    GAMMAI=SEDIMENT_SPECTRAL_SLOPE
ELSE
    STOP 'FREEFORMFLAG VALUE NOT ASSIGNED'
ENDIF


DO IWV_BAND=1,NWV_BAND+1

  IF(IWV_BAND>NWV_BAND)THEN
     WAVELENGTH_MICRON=WAVELENGTH_MICRON_REF
  ELSE
	WAVELENGTH_MICRON=WV_BAND_MICRON(IWV_BAND)
  ENDIF
  CALL SPHER_INTERFACE(NDISTR,REFF1_LOCAL,VEFF1_LOCAL,WAVELENGTH_MICRON,&
                       MRR1(IWV_BAND),MRI1(IWV_BAND),AA1,BB1,AA2,BB2,GAMMAI,&
                       REFFO,VEFFO,AREA,CEXT1,CSCAT1,NANGMIE_LOCAL,PHMXMIE_LOCAL1)
  IF(ARSLND2>0.0D0 .AND. FREEFORMFLAG<=1)THEN
    CALL SPHER_INTERFACE(NDISTR,REFF2_LOCAL,VEFF2_LOCAL,WAVELENGTH_MICRON,&
                       MRR2(IWV_BAND),MRI2(IWV_BAND),AA1,BB1,AA2,BB2,GAMMAI,&
                       REFFO,VEFFO,AREA,CEXT2,CSCAT2,NANGMIE_LOCAL,PHMXMIE_LOCAL2)
  ELSE
      CEXT2=0.0D0
      CSCAT2=0.0D0
  ENDIF
IF(FREEFORMFLAG<=1)THEN
  NUM_SCAT_ANG(1)=NANGMIE_LOCAL
  CEXT_ARSL_BAND(IWV_BAND)=ARSLND1*CEXT1+ARSLND2*CEXT2
  CSCAT_ARSL_BAND(IWV_BAND)=ARSLND1*CSCAT1+ARSLND2*CSCAT2
  IF(IWV_BAND>NWV_BAND)RETURN
  PHMXMIE_ARSL_LandSat(IWV_BAND,:,:)=(ARSLND1*CSCAT1*PHMXMIE_LOCAL1(:,:) + &
                            ARSLND2*CSCAT2*PHMXMIE_LOCAL2(:,:))/ &
                            CSCAT_ARSL_BAND(IWV_BAND)
ELSEIF(FREEFORMFLAG==2)THEN
  NUM_SCAT_ANG(2)=NANGMIE_LOCAL
  CEXT_Plankton_BAND(IWV_BAND)=CEXT1
  CSCAT_Plankton_BAND(IWV_BAND)=CSCAT1
  IF(IWV_BAND>NWV_BAND)RETURN
  PHMXMIE_Plankton_LandSat(IWV_BAND,:,:)=PHMXMIE_LOCAL1(:,:)
  CALL BBFRAC_CAL(NANGMIE_LOCAL,PHMXMIE_LOCAL1,BBFRAC_LOCAL)
  BBFRAC_Plankton_BAND(IWV_BAND)=BBFRAC_LOCAL
ELSEIF(FREEFORMFLAG==3)THEN
  NUM_SCAT_ANG(3)=NANGMIE_LOCAL
  REFF_MINERAL=REFFO
  AREA_MINERAL=AREA
  CEXT_Mineral_BAND(IWV_BAND)=CEXT1
  CSCAT_Mineral_BAND(IWV_BAND)=CSCAT1
  IF(IWV_BAND>NWV_BAND)RETURN
  PHMXMIE_Mineral_LandSat(IWV_BAND,:,:)=PHMXMIE_LOCAL1(:,:)
  CALL BBFRAC_CAL(NANGMIE_LOCAL,PHMXMIE_LOCAL1,BBFRAC_LOCAL)
  BBFRAC_Mineral_BAND(IWV_BAND)=BBFRAC_LOCAL
ENDIF
ENDDO

END SUBROUTINE PHMXMIE_LandSat_INIT

END MODULE

SUBROUTINE BBFRAC_CAL(NANGMIE_LOCAL,PHMXMIE_LOCAL1,BBFRAC_LOCAL)
USE RTUTILITY,ONLY : FACTOR,NUMMIEANGMAX
INTEGER,INTENT(IN) :: NANGMIE_LOCAL
REAL*8,DIMENSION(NUMMIEANGMAX,0:6),INTENT(IN)::PHMXMIE_LOCAL1
REAL*8,INTENT(OUT) :: BBFRAC_LOCAL

INTEGER,DIMENSION(1) ::LOCINDXTMP
INTEGER :: NSIZE
REAL*8, DIMENSION(:),ALLOCATABLE :: XARRAY,YARRAY
REAL*8 :: RTMP,RTMP1,TRAPZOID

ALLOCATE(XARRAY(NANGMIE_LOCAL),YARRAY(NANGMIE_LOCAL))
XARRAY(:)=PHMXMIE_LOCAL1(1:NANGMIE_LOCAL,0)
YARRAY(:)=PHMXMIE_LOCAL1(1:NANGMIE_LOCAL,1)
XARRAY=COS(XARRAY*FACTOR)
RTMP=TRAPZOID(NANGMIE_LOCAL,XARRAY,YARRAY)

LOCINDXTMP =MINLOC(ABS(90.0d0-PHMXMIE_LOCAL1(:,0)))
IF(LOCINDXTMP(1)==1 .OR. LOCINDXTMP(1)>=NANGMIE_LOCAL) STOP 'CHECK BBFRAC_CAL 1'
NSIZE=NANGMIE_LOCAL-LOCINDXTMP(1)+1
DEALLOCATE(XARRAY,YARRAY)

ALLOCATE(XARRAY(NSIZE),YARRAY(NSIZE))
XARRAY(:)=PHMXMIE_LOCAL1(LOCINDXTMP(1):NANGMIE_LOCAL,0)
YARRAY(:)=PHMXMIE_LOCAL1(LOCINDXTMP(1):NANGMIE_LOCAL,1)
XARRAY=COS(XARRAY*FACTOR)
IF(XARRAY(2)>0.0D0 .OR. XARRAY(NSIZE)>0.0D0)STOP 'CHECK BBFRAC_CAL 2'
RTMP1=TRAPZOID(NSIZE,XARRAY,YARRAY)
!WRITE(*,*)'PHASE FUCNTION, NORMALIZATION AND BACKSCATTERING FACTOR=',RTMP,RTMP1
!WRITE(*,*)'PHASE FUCNTION,Ang(90),Ang(180) =',PHMXMIE_LOCAL1(LOCINDXTMP,0),&
!                          PHMXMIE_LOCAL1(NANGMIE_LOCAL,0)
BBFRAC_LOCAL=RTMP1/RTMP
DEALLOCATE(XARRAY,YARRAY)

ENDSUBROUTINE BBFRAC_CAL

SUBROUTINE SPHER_INTERFACE(NDISTR,REFFI,VEFFI,LAM,MRR,MRI,AA1,BB1,AA2,BB2,GAMMAI,&
              REFFO,VEFFO,AREA,CEXT,CSCAT,NANGMIE_LOCAL,PHMXMIE_LOCAL)

USE RTUTILITY,ONLY : NUMMIEANGMAX,PI

IMPLICIT REAL*8 (A-H,O-Z)                           
INTEGER ::  NANGMIE_LOCAL
REAL*8,DIMENSION(NUMMIEANGMAX,0:6)::PHMXMIE_LOCAL

INTEGER,PARAMETER:: NMIE=10000, NPL=2*NMIE
INTEGER NDISTR,LMAXI,IL
REAL*8 REFFI,VEFFI,LAM,MRR,MRI,AA1,BB1,AA2,BB2,GAMMAI,REFFO,VEFFO,AREA,CEXT,CSCAT
REAL*8 AL1(NPL),AL2(NPL),AL3(NPL),AL4(NPL),BET1(NPL),BET2(NPL)

!      CHARACTER*40 :: CFILE1,CHARTMP
!      open (6, file='spher.coeff')
!      AA=0.6D0
!      BB=0.2 D0
!      AA1=0.17D0
!      AA2=3.44D0
!      BB1=DLOG(1.96D0)*DLOG(1.96D0)
!      BB2=DLOG(2.37D0)*DLOG(2.37D0)
!      GAM=1D0                                                               
!      LAM=0.63D0                                            
!      MRR=1.53 D0                                               
!      MRI=0.008 D0                                            
!      NDISTR=3
!      NK=100
!      N=100
!      NP=4
      R1=0.0D0
      R2=20.0D0    

       IF(NDISTR==1) THEN
!     NDISTR = 1 - modified gamma distribution                         
!          [Eq. (5.242) of Ref. 1]                                        
!              AA=alpha                                                
!              BB=r_c                                                  
!              GAM=gamma                                               

            AA=GAMMAI-1
            BB=AA*REFFI/(GAMMAI+2.0D0)
            GAM=1.0D0
            
       ELSE IF(NDISTR==2) THEN
!C     NDISTR = 2 - log normal distribution                             
!C          [Eq. (5.243) of Ref. 1]                                        
!C              AA=r_g                                                  
!C              BB=[ln(sigma_g)]**2                                      
            BB=log(VEFFI+1.0D0)
            AA=REFFI*EXP(-2.5d0*BB)

       ELSE IF(NDISTR==3) THEN
!C     NDISTR = 3 - power law distribution                              
!C          [Eq. (5.244) of Ref. 1]                                        
!C               AA=r_eff (effective radius)                            
!C               BB=v_eff (effective variance)                          
!C               Parameters R1 and R2 (see below) are calculated        
!C               automatically for given AA and BB                      
                AA=REFFI
                BB=VEFFI

       ELSE IF(NDISTR==4) THEN
!C     NDISTR = 4 - gamma distribution                                  
!C          [Eq. (5.245) of Ref. 1]                                        
!C               AA=a                                                   
!C               BB=b      
!http://www.ess.uci.edu/~cmclinden/link/xx/node22.html
!HANSEN AND TRAVIS 1974 EQ. 2.56
                AA=REFFI  
                BB=VEFFI
                                                     
       ELSE IF(NDISTR==5) THEN
!C     NDISTR = 5 - modified power law distribution                     
!C          [Eq. (5.246) of Ref. 1]                                        
!C              BB=alpha          
               BB=GAMMAI
       ELSE IF(NDISTR==6) THEN
!C     NDISTR = 6 - bimodal volume log normal distribution              
!C              [Eq. (5.247) of Ref. 1]             
!C              AA1=r_g1                                                
!C              BB1=[ln(sigma_g1)]**2                                   
!C              AA2=r_g2                                                
!C              BB2=[ln(sigma_g2)]**2                                   
!C              GAM=gamma                                               
               GAM=GAMMAI
!C
       ELSE IF(NDISTR==7) THEN
!C    Added by Zhai, Pengwang Oct. 29 2008
!C     NDISTR = 7 - Junge distribution                              
!C          [n(r)=Constant*r^{-s}; s=4]
!C               Parameters R1 and R2 (see below)
!C               BB=JUNGE EXPONENTIAL FACTOR s                           
                BB=GAMMAI
                R1=0.1D0
                R2=50.0D0
       ELSE
          STOP 'NDISTR<1 OR > 7'
       ENDIF
       NK=20
       N=100
       NP=4
       WVNM=LAM*1000
       DDELT=1D-7

      CALL SPHER (AA,BB,GAM,LAM,MRR,MRI,R1,R2,N,NP,NDISTR,             &
            NK,L1,AL1,AL2,AL3,AL4,BET1,BET2,CEXT,CSCAT,AREA,VOL,RVW,  &
            RMEAN,REFFO,VEFFO,AA1,BB1,AA2,BB2,DDELT)
      QE=CEXT/AREA
      LMAX=L1-1

      LMAXI=LMAX
      CALL MATR (AL1,AL2,AL3,AL4,BET1,BET2,LMAX,NUMMIEANGMAX,NANGMIE_LOCAL,PHMXMIE_LOCAL)
      

!      WRITE (*,1001) AREA,VOL,RVW,RMEAN
! 1001 FORMAT ('<G> = ',D12.6,'  <V> = ',D12.6,'  Rvw = ',D12.6,&
!             &'  <R> = ',D12.6)
!write(*,*)'testing'
!  DO INTTMP=1,LMAXI+1
!      write(*,*)AL1(INTTMP),AL2(INTTMP),AL3(INTTMP),AL4(INTTMP),&
!             BET1(INTTMP),BET2(INTTMP)
!  ENDDO
!write(*,*)'testing over'
  
      RETURN
      END SUBROUTINE SPHER_INTERFACE
