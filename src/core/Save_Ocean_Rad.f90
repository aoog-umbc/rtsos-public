! ===============================================================
!  RTSOS – Radiative Transfer model based on Successive Orders of Scattering
!  Copyright (C) 2025  Pengwang Zhai
!
!  Licensed under the Creative Commons Attribution–NonCommercial 4.0
!  International License (CC BY-NC 4.0).
!  You may use, modify, and share this code for research and
!  educational purposes with proper attribution.
!  Commercial use requires written permission from the author.
!
!  Full license: https://creativecommons.org/licenses/by-nc/4.0/
!  Contact: Pengwang Zhai  |  [pwzhai@gmail.com]
! ===============================================================

! Correction May 18, 2018, corrected DIIRAD_SCALAR_TGD calculation, no need to divide by (4 pi)
MODULE IRRADSUNL
INTEGER :: NSUNL
DOUBLE PRECISION,DIMENSION(:),ALLOCATABLE :: WVSUN,FSUN
INTEGER :: SUNREAD_STATUS = 0

CONTAINS

SUBROUTINE SUNLREADIN(aux_dir)
IMPLICIT NONE

INTEGER :: ILINE
CHARACTER*360 ::aux_dir
CHARACTER(LEN=360)SUNLFILE
DOUBLE PRECISION :: RTMP

LOGICAL :: file_e

IF(TRIM(aux_dir)=='00000')THEN
	OPEN(UNIT=1,FILE='./auxiliary_directory',STATUS='OLD',ACTION='READ')
	READ(1,'(A)')aux_dir
	CLOSE(1)
ENDIF

! Coddington, O. M., Richard, E. C., Harber, D., Pilewskie, P., Woods,
! T. N., Snow, M., et al. (2023).
! Version 2 of the TSIS-1 Hybrid Solar Reference Spectrum and extension to the full spectrum.
! Earth and Space Science, 10, e2022EA002637. https://doi.org/10.1029/2022EA002637
SUNLFILE=trim(aux_dir)//'/tsis1_hsrs_binned_fs.csv'
INQUIRE(FILE=SUNLFILE, EXIST=file_e)

IF(file_e)THEN
! USE TSIS-1 SSI FILE
	NSUNL=199885
	ALLOCATE(WVSUN(NSUNL),FSUN(NSUNL))

	OPEN(UNIT=1,FILE=SUNLFILE,STATUS='OLD',ACTION='READ')
	READ(1,*)
	DO ILINE=1,NSUNL
	 READ(1,*)WVSUN(ILINE),FSUN(ILINE),RTMP
	ENDDO
	CLOSE(1)
	FSUN=FSUN*1000.0D0  ! CHANGE UNIT FROM W/m^2/nm to W/m^2/micron
	WVSUN=WVSUN/1000.0D0 ! CHANGE UNIT FROM nm TO micron
ELSE
	WRITE(*,*)'CANNOT FOUND SOLAR IRRADIANCE FILE:',SUNLFILE
	WRITE(*,*)'Trying to find f0.txt instead ...'

! USE THUILLIER SPECTRUM
	SUNLFILE=trim(aux_dir)//'/f0.txt'
	INQUIRE(FILE=SUNLFILE, EXIST=file_e)
	IF(file_e)THEN

		NSUNL=2202
		ALLOCATE(WVSUN(NSUNL),FSUN(NSUNL))
		OPEN(UNIT=1,FILE=SUNLFILE,STATUS='OLD',ACTION='READ')
		DO ILINE=1,13
		READ(1,*)
		ENDDO

		DO ILINE=1,NSUNL
		 READ(1,*)WVSUN(ILINE),FSUN(ILINE)
		ENDDO
		CLOSE(1)
		FSUN=FSUN*10.0D0  ! CHANGE UNIT FROM mW/cm^2/um to W/m^2/micron
		WVSUN=WVSUN/1000.0D0 ! CHANGE UNIT FROM nm TO micron

	ELSE
		WRITE(*,*)'SOLAR IRRADIANCE FILE IS:',SUNLFILE
		STOP 'CANNOT FOUND SOLAR IRRADIANCE THUILLIER SPECTRUM f0.txt'
	ENDIF

ENDIF

SUNREAD_STATUS = 1
END SUBROUTINE SUNLREADIN

SUBROUTINE FSUNCAL(WAVELENGTH,ESUN1)
IMPLICIT NONE
DOUBLE PRECISION,INTENT(IN):: WAVELENGTH
DOUBLE PRECISION,INTENT(OUT)::ESUN1
CHARACTER*360 ::aux_dir
INTEGER :: IULO,MPL,KLO
DOUBLE PRECISION :: RTMP
DOUBLE PRECISION,DIMENSION(1) :: XI,YI
IF(WAVELENGTH>WVSUN(NSUNL)) THEN
   WRITE(*,*)'WAVELENGTH > MAX(WVSUN) IN FSUNCAL, SETTING ESUN TO BE ZERO'
   ESUN1=0.0D0
   RETURN
ENDIF
IF(SUNREAD_STATUS ==0) THEN
     aux_dir='00000'
     CALL SUNLREADIN(aux_dir)
ENDIF
MPL=2
!IULO=INT(FLOAT(NSUNL)*ABS((WAVELENGTH-WVSUN(1))/(WVSUN(NSUNL)-WVSUN(1))))
!call hunt(WVSUN,NSUNL,WAVELENGTH,IULO)
!KLO=min(max(IULO-(MPL-1)/2,1),NSUNL-MPL)
!CALL POLINT(WVSUN(KLO),FSUN(KLO),MPL,WAVELENGTH,ESUN1,RTMP)
XI=WAVELENGTH
CALL UVIP3P(MPL,NSUNL,WVSUN,FSUN,1,XI,YI)
ESUN1=YI(1)
ENDSUBROUTINE FSUNCAL

SUBROUTINE DEALL_SUNL
DEALLOCATE(WVSUN,FSUN)
END SUBROUTINE DEALL_SUNL

END MODULE IRRADSUNL

MODULE SAVE_OCEAN_MOD
USE RTTYPE
  DOUBLE PRECISION,DIMENSION(:),ALLOCATABLE :: XJSOMAIN
  TYPE(MATRIX44), DIMENSION(:,:,:),ALLOCATABLE :: PMORAMAN,SPMORAMAN
! BETAL(I,J), I=1~NUMMIE,J=1,NUMORD. EXPANSION COEFFICIENTS
  DOUBLE PRECISION,PARAMETER :: WRAMANDEPOL=0.17
  REAL(DP), DIMENSION(0:2):: WRAMANBETAL,WRAMANALPHAL,WRAMANZETAL,&
                             WRAMANDELTAL,WRAMANGAMMAL,WRAMANEPSILONL

! water depth used to form common vertical coordinates for
! both elastic and inelastic scattering.
! optical depth grid cannot be used because
! the optical depth is different for different wavelength
! IT HAS TO BE DENSE TO MAKE SURE INTERPOLATION BETWEEN WATER DEPTH IS ACCURATE.
! SO CHANGE WITH CARE, IT IS SUPPOSED TO IMPACT THE SIMULATION ACCURACY
! NEED TO EXPLORE MORE IN THE FUTURE

  INTEGER,PARAMETER ::NWATER_DEPTH=101
  DOUBLE PRECISION,DIMENSION(NWATER_DEPTH)::WATER_DEPTH
  INTEGER :: NUMTOTALTAUEX,NQUADA_Inelas,NQUADO_Inelas,MAXLORD_Inelas,NXJO_Inelas
  DOUBLE PRECISION :: OITAU_EX,SCATT_COEFF_RAMAN_EX,WATER_DEPTH_MAX
  DOUBLE PRECISION,DIMENSION(4) :: IRRAD_SUN_EX
  DOUBLE PRECISION,DIMENSION(:),ALLOCATABLE :: XJO_Inelas,XJO_MAIN,TAU_INELASTIC_EX
  DOUBLE PRECISION,DIMENSION(:), ALLOCATABLE :: TAU_INELASTIC_AGD,EXT_COEFF_INELASTIC_EM,&
                                      DIIRAD_SCALAR_TGD

  DOUBLE PRECISION,DIMENSION(:,:), ALLOCATABLE :: DIIRAD_SCALAR_AGD,&
                         ABSORB_COEFF_CDOM_EX,ABSORB_COEFF_CHLA_EX
  TYPE(ARRAY4),DIMENSION(:,:,:),ALLOCATABLE :: SOURCEFO_RAMAN_TGD,    &
                                               SOURCEFO_RAMAN_AGD
  TYPE(ARRAY4),DIMENSION(:,:,:,:),ALLOCATABLE :: SOURCEFO_RAMAN_AGD_LUT
  DOUBLE PRECISION,DIMENSION(:),ALLOCATABLE :: SOURCEFO_FLUORESCENCE_AGD
  DOUBLE PRECISION,DIMENSION(:),ALLOCATABLE :: IPAR_SCALAR
  DOUBLE PRECISION,DIMENSION(:),ALLOCATABLE :: QUANTUM_YIELD_CHLA_PROFILE
  DOUBLE PRECISION,DIMENSION(:),ALLOCATABLE :: CHLA_VERTICAL_PROFILE

END MODULE SAVE_OCEAN_MOD

SUBROUTINE XJOMAININIT(NQUADATMP,NQUADOTMP)
USE SAVE_OCEAN_MOD
INTEGER, INTENT(IN) :: NQUADATMP,NQUADOTMP
INTEGER :: IQUAD, JQUAD
DOUBLE PRECISION,DIMENSION(:),ALLOCATABLE :: XJA_MAIN,XJTMP,GAUWTTMP
DOUBLE PRECISION :: X1, X2

ALLOCATE(XJA_MAIN(NQUADATMP+2),XJO_MAIN(NQUADOTMP+2))
X1=0.0d0
X2=1.0d0

ALLOCATE(XJTMP(NQUADATMP/2),GAUWTTMP(NQUADATMP/2))
CALL gauss_legendre(X1,X2,XJTMP,GAUWTTMP,NQUADATMP/2)
  XJA_MAIN(1)=-1.0D0
  XJA_MAIN(NQUADATMP+2)=1.0D0
  DO IQUAD=1,NQUADATMP/2
    JQUAD=IQUAD+1
! GAUSSIAN QUADRATURE AND WEIGHT IN ATMOSPHERE
    XJA_MAIN(JQUAD)=-XJTMP(NQUADATMP/2+1-IQUAD)
    XJA_MAIN(NQUADATMP+2-JQUAD+1)=-XJA_MAIN(JQUAD)
  ENDDO
  DEALLOCATE(XJTMP,GAUWTTMP)

	DO IQUAD=1,NQUADATMP/2+1
	  THETA1=PI-DACOS(XJA_MAIN(IQUAD))
      RTMP=SIN(THETA1)/1.338D0
	  RTMP=COS(ASIN(RTMP))
	  XJO_MAIN(IQUAD)=-RTMP
	  XJO_MAIN(NQUADOTMP+2-IQUAD+1)=-XJO_MAIN(IQUAD)
	ENDDO

	ALLOCATE(XJTMP((NQUADOTMP-NQUADATMP)/2),GAUWTTMP((NQUADOTMP-NQUADATMP)/2))
    X1=-COS(ASIN(1.0D0/1.338))
	X2=0.0d0
	call gauss_legendre(X1,X2,XJTMP,GAUWTTMP,(NQUADOTMP-NQUADATMP)/2)

	DO IQUAD=1,(NQUADOTMP-NQUADATMP)/2
	  XJO_MAIN(IQUAD+NQUADATMP/2+1)=XJTMP(IQUAD)
	  XJO_MAIN(NQUADOTMP+2-IQUAD-NQUADATMP/2) = - XJO_MAIN(IQUAD+NQUADATMP/2+1)
	ENDDO

	DEALLOCATE(XJTMP,GAUWTTMP)

!DO IQUAD=1,NQUADOTMP+2
!WRITE(*,*)'TESTING XQUADO_MAIN',IQUAD, XJO_MAIN(IQUAD)
!ENDDO

DEALLOCATE(XJA_MAIN)
END SUBROUTINE


SUBROUTINE TAU_EX_ALLO
USE RTTYPE
USE RTUTILITY
USE SAVE_OCEAN_MOD
IMPLICIT NONE
NUMTOTALTAUEX=NTTAU-OLYR(1)%ITAUS+1
OITAU_EX=OITAU_PRSV
IRRAD_SUN_EX=ESUN
ALLOCATE(TAU_INELASTIC_EX(NUMTOTALTAUEX))
TAU_INELASTIC_EX(1:NUMTOTALTAUEX)=TAU_PRSV(OLYR(1)%ITAUS:NTTAU)-OITAU_PRSV
ENDSUBROUTINE

SUBROUTINE SAVE_OCEAN_RAD(MU_IN)
USE RTTYPE
USE RTUTILITY
USE SAVE_OCEAN_MOD
IMPLICIT NONE
DOUBLE PRECISION,INTENT(IN) :: MU_IN
INTEGER :: MAXM_LOCAL
INTEGER :: ITAU,IQUAD,JQUAD,MVAR,I,ICOM
DOUBLE PRECISION :: THETA1,RTMP1,THETA2
DOUBLE PRECISION,DIMENSION(4,4) :: MTRX1,FRES_MTRX
DOUBLE PRECISION,DIMENSION(4):: ARRY1,ARRY2,ARRY3

IF(.NOT. DOUBLE_GAUSSIAN_QUAD)THEN
	WRITE(*,*) 'DOUBLE_GAUSSIAN_QUAD IS .FALSE. FOR INELASTIC SCATTERING'
    WRITE(*,*) 'This is not good as xjo_main will not match xjo in elastic case'
	WRITE(*,*)'RESET IT TO BE .TRUE.'
    STOP
ENDIF

MAXM_LOCAL=MIN(MAXMORD,2)

ALLOCATE(XJSOMAIN(1))
THETA1=ACOS(ABS(MU_IN))
THETA2=ASIN(SIN(THETA1)/NWRE)
XJSOMAIN=-COS(THETA2)

NXJO_Inelas=NQUADO
ALLOCATE(XJO_Inelas(NXJO_Inelas))
XJO_Inelas=XJO

    ALLOCATE(SOURCEFO_RAMAN_TGD(NQUADO,NUMTOTALTAUEX,0:2))
    DO ICOM=1,4
       SOURCEFO_RAMAN_TGD(1:NQUADO,1:NUMTOTALTAUEX,0:2)%VRAD(ICOM)=0.0D0
    ENDDO

    DO IQUAD=1,NQUADO
    DO JQUAD=1,NQUADO
    DO MVAR=0,MAXM_LOCAL
      MTRX1=PMORAMAN(IQUAD,JQUAD,MVAR)%PHMX
      DO ITAU=OLYR(1)%ITAUS,OLYR(NOLYR)%ITAUE
        ARRY1=LSPO(JQUAD,ITAU,MVAR)%VRAD
        ARRY2=MATMUL(MTRX1,ARRY1)
        SOURCEFO_RAMAN_TGD(IQUAD,ITAU-OLYR(1)%ITAUS+1,MVAR)%VRAD=    &
          SOURCEFO_RAMAN_TGD(IQUAD,ITAU-OLYR(1)%ITAUS+1,MVAR)%VRAD + &
                  0.5D0*WTO(JQUAD)*ARRY2
      ENDDO ! LOOP SUBLAYERS
    ENDDO   !LOOP MVAR
    ENDDO   !LOOP JQUAD
    ENDDO   ! LOOP IQUAD



THETA1=PI-ACOS(MU_IN)
CALL FRSNL_T2(NWRE,NWIM,THETA1,FRES_MTRX)

ARRY1=ESUN*EXP(OITAU/MU_IN)/PI
MTRX1=FRES_MTRX*abs(MU_IN/XJSOMAIN(1))/4.0d0
ARRY2=MATMUL(MTRX1,ARRY1)
IF(WCFLG)ARRY2=ARRY2*(1.0D0-FWC)

     DO MVAR=0,MAXM_LOCAL
     DO IQUAD=1,NQUADO

     MTRX1=SPMORAMAN(1,IQUAD,MVAR)%PHMX
     ARRY3=MATMUL(MTRX1,ARRY2)

     DO ITAU=OLYR(1)%ITAUS,OLYR(NOLYR)%ITAUE

       RTMP1=EXP(-ABS((TAU(ITAU)-OITAU)/XJSOMAIN(1)))

       SOURCEFO_RAMAN_TGD(IQUAD,ITAU-OLYR(1)%ITAUS+1,MVAR)%VRAD =     &
           SOURCEFO_RAMAN_TGD(IQUAD,ITAU-OLYR(1)%ITAUS+1,MVAR)%VRAD + &
                  RTMP1*ARRY3
     ENDDO
     ENDDO
     ENDDO
DEALLOCATE(XJSOMAIN)
ENDSUBROUTINE

SUBROUTINE SAVE_OCEAN_SCALAR_IRRAD(MU_IN)
USE RTTYPE
USE RTUTILITY
USE SAVE_OCEAN_MOD
IMPLICIT NONE
DOUBLE PRECISION,INTENT(IN) :: MU_IN
INTEGER :: ITAU,IQUAD,JQUAD,MVAR,I,ICOM
DOUBLE PRECISION :: THETA1,RTMP1,THETA2
DOUBLE PRECISION,DIMENSION(4,4) :: MTRX1,FRES_MTRX
DOUBLE PRECISION,DIMENSION(4):: ARRY1,ARRY2,ARRY3

ALLOCATE(XJSOMAIN(1))
THETA1=ACOS(ABS(MU_IN))
THETA2=ASIN(SIN(THETA1)/NWRE)
XJSOMAIN=-COS(THETA2)

ALLOCATE(DIIRAD_SCALAR_TGD(NUMTOTALTAUEX))
DIIRAD_SCALAR_TGD=0.0D0
DO JQUAD=1,NQUADO
DO ITAU=OLYR(1)%ITAUS,OLYR(NOLYR)%ITAUE
   DIIRAD_SCALAR_TGD(ITAU-OLYR(1)%ITAUS+1)=    &
            DIIRAD_SCALAR_TGD(ITAU-OLYR(1)%ITAUS+1) + &
                  0.5D0*WTO(JQUAD)*LSPO(JQUAD,ITAU,0)%VRAD(1)
ENDDO ! LOOP SUBLAYERS
ENDDO   !LOOP JQUAD

THETA1=PI-ACOS(MU_IN)
CALL FRSNL_T2(NWRE,NWIM,THETA1,FRES_MTRX)

ARRY1=ESUN*abs(MU_IN/XJSOMAIN(1))*EXP(OITAU/MU_IN)
MTRX1=FRES_MTRX

ARRY2=MATMUL(MTRX1,ARRY1)
IF(WCFLG)ARRY2=ARRY2*(1.0D0-FWC)
!write(*,*)'testing DIIRAD_SCALAR_TGD calculation',DIIRAD_SCALAR_TGD(1),ARRY2(1),&
!                              EXP(-ABS((TAU(OLYR(1)%ITAUS)-OITAU)/XJSOMAIN(1)))
DO ITAU=OLYR(1)%ITAUS,OLYR(NOLYR)%ITAUE

    RTMP1=EXP(-ABS((TAU(ITAU)-OITAU)/XJSOMAIN(1)))

    DIIRAD_SCALAR_TGD(ITAU-OLYR(1)%ITAUS+1)=          &
            DIIRAD_SCALAR_TGD(ITAU-OLYR(1)%ITAUS+1) + RTMP1*ARRY2(1)
ENDDO

DEALLOCATE(XJSOMAIN)
ENDSUBROUTINE


SUBROUTINE PMARAMANCAL(MU_IN,NQUADAINPUT,NQUADOINPUT,WVLN,TMPTR,SALINITY)
USE RTTYPE
USE RTUTILITY
USE RAMANDATA
USE SAVE_OCEAN_MOD
IMPLICIT NONE

INTEGER,INTENT(IN) ::NQUADAINPUT,NQUADOINPUT
DOUBLE PRECISION,INTENT(IN):: WVLN ! WAVELENGTH IN NANOMETERS.
DOUBLE PRECISION,INTENT(IN):: MU_IN,TMPTR,SALINITY
DOUBLE PRECISION,DIMENSION(:),ALLOCATABLE :: XJAMAIN,WTAMAIN,XJOMAIN,WTOMAIN
INTEGER :: NQUADAP2,NQUADOP2,NQUADDIFMAIN,NQUADSOMAIN
INTEGER :: IQUAD,JQUAD,L,M
DOUBLE PRECISION :: RINDX_WATER
LOGICAL :: SCLMAIN

DOUBLE PRECISION,DIMENSION(:,:,:),ALLOCATABLE:: DML0OLOCAL,DML2POLOCAL,DML2NOLOCAL,&
                         DML0SOLOCAL,DML2PSOLOCAL,DML2NSOLOCAL
DOUBLE PRECISION,DIMENSION(:,:),ALLOCATABLE:: WD0,WD2,WDN2
DOUBLE PRECISION,DIMENSION(:),ALLOCATABLE:: XJTMP,GAUWTTMP

DOUBLE PRECISION :: U,NEG1,NEG1LM,THETA1,THETA2,X1,X2,RTMP

CALL WATER_REFRACTIVE_INDEX(WVLN,TMPTR,SALINITY,RINDX_WATER)

SCLMAIN=.FALSE.

NQUADSOMAIN=1
NQUADAP2=NQUADAINPUT+2
NQUADOP2=NQUADOINPUT+2
ALLOCATE(XJAMAIN(NQUADAP2),WTAMAIN(NQUADAP2),XJOMAIN(NQUADOP2),WTOMAIN(NQUADOP2))
X1=0.0d0
X2=1.0d0

  ALLOCATE(XJTMP(NQUADAP2/2-1),GAUWTTMP(NQUADAP2/2-1))
  call gauss_legendre(X1,X2,XJTMP,GAUWTTMP,NQUADAP2/2-1)
  XJAMAIN(1)=-1.0D0
  WTAMAIN(1)=0.0D0
  XJAMAIN(NQUADAP2)=1.0D0
  WTAMAIN(NQUADAP2)=0.0D0
  
  DO IQUAD=1,NQUADAP2/2-1
    JQUAD=IQUAD+1
! GAUSSIAN QUADRATURE AND WEIGHT IN ATMOSPHERE 
    XJAMAIN(JQUAD)=-XJTMP(NQUADAP2/2-IQUAD)
    XJAMAIN(NQUADAP2-JQUAD+1)=-XJAMAIN(JQUAD)
    WTAMAIN(JQUAD)=GAUWTTMP(NQUADAP2/2-IQUAD)
    WTAMAIN(NQUADAP2-JQUAD+1)=WTAMAIN(JQUAD)
  ENDDO

DEALLOCATE(XJTMP,GAUWTTMP)
! GAUSSIAN QUADRATURE AND WEIGHT IN OCEAN

DO IQUAD=1,NQUADAP2/2
  THETA1=PI-DACOS(XJAMAIN(IQUAD))
  RTMP=SIN(THETA1)/RINDX_WATER
  RTMP=COS(ASIN(RTMP))
  XJOMAIN(IQUAD)=-RTMP
  WTOMAIN(IQUAD)=WTAMAIN(IQUAD)*                     &
         DABS(XJAMAIN(IQUAD)/XJOMAIN(IQUAD))/RINDX_WATER/RINDX_WATER
  XJOMAIN(NQUADOP2-IQUAD+1)=-XJOMAIN(IQUAD)
  WTOMAIN(NQUADOP2-IQUAD+1)=WTOMAIN(IQUAD)
ENDDO
NQUADDIFMAIN=NQUADOP2-NQUADAP2

ALLOCATE(XJTMP(NQUADDIFMAIN/2),GAUWTTMP(NQUADDIFMAIN/2))
X1=-COS(ASIN(1.0D0/RINDX_WATER))
X2=0.0d0
call gauss_legendre(X1,X2,XJTMP,GAUWTTMP,NQUADDIFMAIN/2)

  DO IQUAD=1,NQUADDIFMAIN/2
    XJOMAIN(IQUAD+NQUADAP2/2)=XJTMP(IQUAD)
    XJOMAIN(NQUADDIFMAIN - IQUAD + 1 + NQUADAP2/2) = - XJOMAIN(IQUAD+NQUADAP2/2)

    WTOMAIN(IQUAD+NQUADAP2/2)=GAUWTTMP(IQUAD)
    WTOMAIN(NQUADDIFMAIN - IQUAD + 1 + NQUADAP2/2)=WTOMAIN(IQUAD+NQUADAP2/2)
  ENDDO
  DEALLOCATE(XJTMP,GAUWTTMP)

WRAMANBETAL=(/1.0d0,-0.1980907d0,0.0d0/)
WRAMANALPHAL=(/0.0d0,0.0d0,1.7369154d0/)
WRAMANZETAL=(/0.0d0,0.0d0,-0.2169535d0/)
WRAMANDELTAL=(/-0.0326276d0,0.4941307d0,-0.0579673d0/)
WRAMANGAMMAL=(/0.0d0,0.0d0,-0.8375312d0/)
WRAMANEPSILONL=(/0.0d0,0.0d0,0.0d0/)

ALLOCATE(DML0OLOCAL(0:2,NQUADOP2,0:2),       &
         DML2POLOCAL(0:2,NQUADOP2,0:2),      &
         DML2NOLOCAL(0:2,NQUADOP2,0:2),      &
         DML0SOLOCAL(0:2,NQUADSOMAIN,0:2),       &
         DML2PSOLOCAL(0:2,NQUADSOMAIN,0:2),      &
         DML2NSOLOCAL(0:2,NQUADSOMAIN,0:2))
ALLOCATE(WD0(0:2,0:2),WD2(0:2,0:2),WDN2(0:2,0:2) )

CALL DMLINIT(2,2)

WD0=0.0D0
WD2=0.0D0
WDN2=0.0D0
NEG1=-1.0D0
!LOOP OVER VARIABLE MU
LOOP_QUADO : DO IQUAD=1,NQUADOP2/2
  U=XJOMAIN(IQUAD)
  CALL DMLSPEC(U,2,2,WD0,WD2,WDN2)

  DO M=0,2
  DO L=M,2
    NEG1LM=NEG1**(L+M)

    DML0OLOCAL(L,IQUAD,M)=WD0(L,M)
    DML2POLOCAL(L,IQUAD,M)=0.5D0*(WD2(L,M) + WDN2(L,M))
    DML2NOLOCAL(L,IQUAD,M)=0.5D0*(WD2(L,M) - WDN2(L,M))

    DML0OLOCAL(L,NQUADOP2-IQUAD+1,M)=NEG1LM*DML0OLOCAL(L,IQUAD,M)
    DML2POLOCAL(L,NQUADOP2-IQUAD+1,M)=NEG1LM*DML2POLOCAL(L,IQUAD,M)
    DML2NOLOCAL(L,NQUADOP2-IQUAD+1,M)=-NEG1LM*DML2NOLOCAL(L,IQUAD,M)
  
  ENDDO  ! LOOP L OVER
  ENDDO   ! LOOP M OVER

ENDDO LOOP_QUADO  ! LOOP OVER NQUAD

ALLOCATE(XJSOMAIN(1))
THETA1=ACOS(ABS(MU_IN))
THETA2=ASIN(SIN(THETA1)/RINDX_WATER)
XJSOMAIN=-COS(THETA2)
!LOOP OVER VARIABLE MU	  
LOOP_QUADSO : DO IQUAD=1,NQUADSOMAIN !NQUADSO
  U=XJSOMAIN(IQUAD)
  CALL DMLSPEC(U,2,2,WD0,WD2,WDN2)
DO M=0,2
DO L=M,2
!  GET DML2P AND DML2N FROM WD AND WD2.
! A FACTOR OF (-1)^{-M} IS IGNORED IN THE FOLLOWING EQUATION
! BECAUSE DML2P AND DML2N APPEAR IN PAIRS IN P^M (SUBROUTINE GETPM ).
! (-1)^{-M} * (-1)^{-M} = 1
  DML0SOLOCAL(L,IQUAD,M)=WD0(L,M)
  DML2PSOLOCAL(L,IQUAD,M)=0.5D0*(WD2(L,M) + WDN2(L,M))
  DML2NSOLOCAL(L,IQUAD,M)=0.5D0*(WD2(L,M) - WDN2(L,M))
ENDDO  ! LOOP L OVER
ENDDO   ! LOOP M OVER
ENDDO LOOP_QUADSO  ! LOOP OVER NQUAD

DEALLOCATE(DMM0FCTR,DMM2FCTR,COEF1,COEF2,COEF3,COEF4, &
           COEF5,COEF6,WD0,WD2,WDN2)

ALLOCATE(PMORAMAN(NQUADOP2,NQUADOP2,0:2))
ALLOCATE(SPMORAMAN(NQUADSOMAIN,NQUADOP2,0:2))

!IF(RAMAN_SOURCE_FLAG)DEALLOCATE(SPMORAMAN)
!IF(RAMAN_SOURCE_FLAG)DEALLOCATE(PMORAMAN)

DO IQUAD=1,NQUADOP2
DO JQUAD=1,NQUADOP2
DO M=0,2

PMORAMAN(IQUAD,JQUAD,M)%PHMX(:,:)=0.0D0

DO L=M,2

  PMORAMAN(IQUAD,JQUAD,M)%PHMX(1,1)=PMORAMAN(IQUAD,JQUAD,M)%PHMX(1,1)+ &
                 WRAMANBETAL(L)*DML0OLOCAL(L,IQUAD,M)*DML0OLOCAL(L,JQUAD,M)
  IF(SCLMAIN)CYCLE
  PMORAMAN(IQUAD,JQUAD,M)%PHMX(1,2)=PMORAMAN(IQUAD,JQUAD,M)%PHMX(1,2)+ &
                 WRAMANGAMMAL(L)*DML0OLOCAL(L,IQUAD,M)*DML2POLOCAL(L,JQUAD,M)
  PMORAMAN(IQUAD,JQUAD,M)%PHMX(1,3)=PMORAMAN(IQUAD,JQUAD,M)%PHMX(1,3) - &
                 WRAMANGAMMAL(L)*DML0OLOCAL(L,IQUAD,M)*DML2NOLOCAL(L,JQUAD,M)

  PMORAMAN(IQUAD,JQUAD,M)%PHMX(2,1)=PMORAMAN(IQUAD,JQUAD,M)%PHMX(2,1)+ &
                 WRAMANGAMMAL(L)*DML2POLOCAL(L,IQUAD,M)*DML0OLOCAL(L,JQUAD,M)
  PMORAMAN(IQUAD,JQUAD,M)%PHMX(2,2)=PMORAMAN(IQUAD,JQUAD,M)%PHMX(2,2)+ &
            WRAMANALPHAL(L)*DML2POLOCAL(L,IQUAD,M)*DML2POLOCAL(L,JQUAD,M) + &
			WRAMANZETAL(L)*DML2NOLOCAL(L,IQUAD,M)*DML2NOLOCAL(L,JQUAD,M)
  PMORAMAN(IQUAD,JQUAD,M)%PHMX(2,3)=PMORAMAN(IQUAD,JQUAD,M)%PHMX(2,3)  - &
		    WRAMANALPHAL(L)*DML2POLOCAL(L,IQUAD,M)*DML2NOLOCAL(L,JQUAD,M) - &
			WRAMANZETAL(L)*DML2NOLOCAL(L,IQUAD,M)*DML2POLOCAL(L,JQUAD,M)
  PMORAMAN(IQUAD,JQUAD,M)%PHMX(2,4)=PMORAMAN(IQUAD,JQUAD,M)%PHMX(2,4) + &
                 WRAMANEPSILONL(L)*DML2NOLOCAL(L,IQUAD,M)*DML0OLOCAL(L,JQUAD,M)
  

  PMORAMAN(IQUAD,JQUAD,M)%PHMX(3,1)=PMORAMAN(IQUAD,JQUAD,M)%PHMX(3,1) - &
                 WRAMANGAMMAL(L)*DML2NOLOCAL(L,IQUAD,M)*DML0OLOCAL(L,JQUAD,M)
  PMORAMAN(IQUAD,JQUAD,M)%PHMX(3,2)=PMORAMAN(IQUAD,JQUAD,M)%PHMX(3,2)  - &
		    WRAMANALPHAL(L)*DML2NOLOCAL(L,IQUAD,M)*DML2POLOCAL(L,JQUAD,M) - &
			WRAMANZETAL(L)*DML2POLOCAL(L,IQUAD,M)*DML2NOLOCAL(L,JQUAD,M)
  PMORAMAN(IQUAD,JQUAD,M)%PHMX(3,3)=PMORAMAN(IQUAD,JQUAD,M)%PHMX(3,3)  + &
            WRAMANALPHAL(L)*DML2NOLOCAL(L,IQUAD,M)*DML2NOLOCAL(L,JQUAD,M) + &
			WRAMANZETAL(L)*DML2POLOCAL(L,IQUAD,M)*DML2POLOCAL(L,JQUAD,M)
  PMORAMAN(IQUAD,JQUAD,M)%PHMX(3,4)=PMORAMAN(IQUAD,JQUAD,M)%PHMX(3,4)  - &
                 WRAMANEPSILONL(L)*DML2POLOCAL(L,IQUAD,M)*DML0OLOCAL(L,JQUAD,M)
  
  PMORAMAN(IQUAD,JQUAD,M)%PHMX(4,2)=PMORAMAN(IQUAD,JQUAD,M)%PHMX(4,2)  - &
                 WRAMANEPSILONL(L)*DML0OLOCAL(L,IQUAD,M)*DML2NOLOCAL(L,JQUAD,M)
  PMORAMAN(IQUAD,JQUAD,M)%PHMX(4,3)=PMORAMAN(IQUAD,JQUAD,M)%PHMX(4,3)  + &
                 WRAMANEPSILONL(L)*DML0OLOCAL(L,IQUAD,M)*DML2POLOCAL(L,JQUAD,M)
  PMORAMAN(IQUAD,JQUAD,M)%PHMX(4,4)=PMORAMAN(IQUAD,JQUAD,M)%PHMX(4,4)  + &
                 WRAMANDELTAL(L)*DML0OLOCAL(L,IQUAD,M)*DML0OLOCAL(L,JQUAD,M)

ENDDO
ENDDO
ENDDO
ENDDO


DO IQUAD=1,NQUADSOMAIN
DO JQUAD=1,NQUADOP2
DO M=0,2

SPMORAMAN(IQUAD,JQUAD,M)%PHMX(:,:)=0.0D0

DO L=M,2

  SPMORAMAN(IQUAD,JQUAD,M)%PHMX(1,1)=SPMORAMAN(IQUAD,JQUAD,M)%PHMX(1,1)+ &
                 WRAMANBETAL(L)*DML0OLOCAL(L,JQUAD,M)*DML0SOLOCAL(L,IQUAD,M)

  IF(SCLMAIN)CYCLE
  
  SPMORAMAN(IQUAD,JQUAD,M)%PHMX(1,2)=SPMORAMAN(IQUAD,JQUAD,M)%PHMX(1,2)+ &
                 WRAMANGAMMAL(L)*DML0OLOCAL(L,JQUAD,M)*DML2PSOLOCAL(L,IQUAD,M)
  SPMORAMAN(IQUAD,JQUAD,M)%PHMX(1,3)=SPMORAMAN(IQUAD,JQUAD,M)%PHMX(1,3) - &
                 WRAMANGAMMAL(L)*DML0OLOCAL(L,JQUAD,M)*DML2NSOLOCAL(L,IQUAD,M)

  SPMORAMAN(IQUAD,JQUAD,M)%PHMX(2,1)=SPMORAMAN(IQUAD,JQUAD,M)%PHMX(2,1)+ &
                 WRAMANGAMMAL(L)*DML2POLOCAL(L,JQUAD,M)*DML0SOLOCAL(L,IQUAD,M)
  SPMORAMAN(IQUAD,JQUAD,M)%PHMX(2,2)=SPMORAMAN(IQUAD,JQUAD,M)%PHMX(2,2)+ &
            WRAMANALPHAL(L)*DML2POLOCAL(L,JQUAD,M)*DML2PSOLOCAL(L,IQUAD,M) + &
			WRAMANZETAL(L) *DML2NOLOCAL(L,JQUAD,M)*DML2NSOLOCAL(L,IQUAD,M)
  SPMORAMAN(IQUAD,JQUAD,M)%PHMX(2,3)=SPMORAMAN(IQUAD,JQUAD,M)%PHMX(2,3)  - &
		    WRAMANALPHAL(L)*DML2POLOCAL(L,JQUAD,M)*DML2NSOLOCAL(L,IQUAD,M) - &
			WRAMANZETAL(L) *DML2NOLOCAL(L,JQUAD,M)*DML2PSOLOCAL(L,IQUAD,M)
  SPMORAMAN(IQUAD,JQUAD,M)%PHMX(2,4)=SPMORAMAN(IQUAD,JQUAD,M)%PHMX(2,4) + &
                 WRAMANEPSILONL(L)*DML2NOLOCAL(L,JQUAD,M)*DML0SOLOCAL(L,IQUAD,M)

  SPMORAMAN(IQUAD,JQUAD,M)%PHMX(3,1)=SPMORAMAN(IQUAD,JQUAD,M)%PHMX(3,1) - &
                 WRAMANGAMMAL(L)*DML2NOLOCAL(L,JQUAD,M)*DML0SOLOCAL(L,IQUAD,M)
  SPMORAMAN(IQUAD,JQUAD,M)%PHMX(3,2)=SPMORAMAN(IQUAD,JQUAD,M)%PHMX(3,2)  - &
		    WRAMANALPHAL(L)*DML2NOLOCAL(L,JQUAD,M)*DML2PSOLOCAL(L,IQUAD,M) - &
			WRAMANZETAL(L) *DML2POLOCAL(L,JQUAD,M)*DML2NSOLOCAL(L,IQUAD,M)
  SPMORAMAN(IQUAD,JQUAD,M)%PHMX(3,3)=SPMORAMAN(IQUAD,JQUAD,M)%PHMX(3,3)  + &
            WRAMANALPHAL(L)*DML2NOLOCAL(L,JQUAD,M)*DML2NSOLOCAL(L,IQUAD,M) + &
			WRAMANZETAL(L) *DML2POLOCAL(L,JQUAD,M)*DML2PSOLOCAL(L,IQUAD,M)
  SPMORAMAN(IQUAD,JQUAD,M)%PHMX(3,4)=SPMORAMAN(IQUAD,JQUAD,M)%PHMX(3,4)  - &
                 WRAMANEPSILONL(L)*DML2POLOCAL(L,JQUAD,M)*DML0SOLOCAL(L,IQUAD,M)

  SPMORAMAN(IQUAD,JQUAD,M)%PHMX(4,2)=SPMORAMAN(IQUAD,JQUAD,M)%PHMX(4,2)  - &
                 WRAMANEPSILONL(L)*DML0OLOCAL(L,JQUAD,M)*DML2NSOLOCAL(L,IQUAD,M)
  SPMORAMAN(IQUAD,JQUAD,M)%PHMX(4,3)=SPMORAMAN(IQUAD,JQUAD,M)%PHMX(4,3)  + &
                 WRAMANEPSILONL(L)*DML0OLOCAL(L,JQUAD,M)*DML2PSOLOCAL(L,IQUAD,M)
  SPMORAMAN(IQUAD,JQUAD,M)%PHMX(4,4)=SPMORAMAN(IQUAD,JQUAD,M)%PHMX(4,4)  + &
                 WRAMANDELTAL(L)*DML0OLOCAL(L,JQUAD,M)*DML0SOLOCAL(L,IQUAD,M)
ENDDO
ENDDO
ENDDO

ENDDO
DEALLOCATE(DML0OLOCAL,DML2NOLOCAL,DML2POLOCAL,DML0SOLOCAL,DML2NSOLOCAL,DML2PSOLOCAL,&
           XJAMAIN,WTAMAIN,XJOMAIN,WTOMAIN,XJSOMAIN)

ENDSUBROUTINE PMARAMANCAL

SUBROUTINE WATER_REFRACTIVE_INDEX(WVLN,tempr,salinity,RINDX_WATER)
IMPLICIT NONE
DOUBLE PRECISION,INTENT(IN) :: WVLN,tempr,salinity
DOUBLE PRECISION,INTENT(OUT) :: RINDX_WATER
INTEGER, PARAMETER :: NWV_MRWATER_LUT=14
DOUBLE PRECISION,PARAMETER,DIMENSION(NWV_MRWATER_LUT)::                          &
 WV_MRWATER_LUT=(/1100.0d0,1200.0d0,1240.0d0,1300.0d0,1400.0d0,1500.0d0,&
              1600.0d0,1640.0d0,1700.0d0,1800.0d0,1900.0d0,2000.0d0,    &
              2100.0d0,2250.0d0/),  &
 MRWATER_LUT=(/1.31984663d0, 1.31799853d0, 1.31723988d0, 1.31605601d0,  &
               1.31386280d0, 1.31208599d0, 1.30963135d0, 1.30856419d0,   &
               1.30677140d0, 1.30337608d0, 1.29904377d0, 1.29690850d0,   &
               1.29183698d0, 1.28199315d0/)
DOUBLE PRECISION,PARAMETER,DIMENSION(10):: &
 ncoeff=(/1.31405d0, 1.779d-4, -1.05d-6, 1.6d-8, -2.02d-6, 15.868d0,    &
          0.01155d0, -0.00423d0, -4382d0,1.1455d6/)

DOUBLE PRECISION :: Func_UVIP3P

INTEGER :: MPL
DOUBLE PRECISION :: RTMP

IF(WVLN>1100.0D0) THEN
  MPL=2
  RINDX_WATER=Func_UVIP3P(NWV_MRWATER_LUT,WV_MRWATER_LUT,MRWATER_LUT,WVLN,MPL)
ELSE
! Quan and Fry's parameterization for index of refraction for sea water
! the empirical formula is good for 200nm<wavelength<1100 nm
! salinity < 35 parts per thousand, 0 degree < temperature < 30 degree.

  RINDX_WATER=ncoeff(1)+(ncoeff(2)+ncoeff(3)*tempr             &
        +  ncoeff(4)*tempr**2)*salinity+ncoeff(5)*tempr**2         &
        + (ncoeff(6)+ncoeff(7)*salinity +ncoeff(8)*tempr)/WVLN  &
        +  ncoeff(9)/WVLN/WVLN+ncoeff(10)/(WVLN**3);
ENDIF
RETURN
END SUBROUTINE WATER_REFRACTIVE_INDEX
