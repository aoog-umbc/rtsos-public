MODULE Atmosphere_Profile
IMPLICIT NONE

INTEGER,PARAMETER :: NPGRID_Atmosphere_Profile=50
REAL*8,DIMENSION(NPGRID_Atmosphere_Profile) :: PRESSURE_AP,Z_FIELD_AP,T_FIELD_AP, &
                  NUMDEN_TOTAL_FIELD_AP,VMR_OZONE_FIELD_AP,VMR_OXYGEN_FIELD_AP,&
                  VMR_H2O_FIELD_AP,VMR_CO2_FIELD_AP,VMR_NO2_FIELD_AP,       &
                  VMR_CH4_FIELD_AP

INTEGER :: NPGRID
REAL*8,DIMENSION(:),ALLOCATABLE :: P_GRID,Z_FIELD,TEM_FIELD,  &
			NUMDEN_TOTAL_FIELD,VMR_OZONE_FIELD,VMR_OXYGEN_FIELD,     &
			VMR_H2O_FIELD,VMR_CO2_FIELD,VMR_NO2_FIELD,VMR_CH4_FIELD

REAL*8, PARAMETER :: AvogadroN=6.022E23              ! molecules mol^{-1}
REAL*8, PARAMETER :: MolarWeight_H2O=18.016              ! kg/kmol

REAL*8 :: OZONE_STD_COLUMN ! Whole column OZONE IN THE INPUT atmosphere IN DOBSON UNIT
REAL*8 :: H2O_STD_COLUMN   ! Whole column WATER VAPOR THE INPUT atmosphere IN CENTIMETERS.
REAL*8 :: NO2_STD_COLUMN !  Whole column NO2 IN THE INPUT atmosphere IN DOBSON UNIT


CONTAINS
SUBROUTINE Atmosphere_Profile_READIN(atmos_profile_file)

CHARACTER*180,INTENT(in) ::atmos_profile_file

CHARACTER*180 ::atmos_dir,atmos_ch4_file,atmos_profile_file_full
INTEGER,PARAMETER :: NHEADER=2
REAL*8,DIMENSION(9) :: RARRY

INTEGER :: ITEMP

OPEN(unit=1,file='gas_absorption_coeff_dir',status='old');
READ(1,'(A)')atmos_dir
CLOSE(1)
atmos_dir=TRIM(atmos_dir)//'/atmmod/'
!atmos_profile_file='afglus.dat';
atmos_profile_file_full=trim(atmos_dir)//trim(atmos_profile_file);
atmos_ch4_file='afglus_ch4_vmr.dat';
atmos_ch4_file=trim(atmos_dir)//trim(atmos_ch4_file);

!write(*,*)'atmos_profile_file_full=',atmos_profile_file_full

OPEN(unit=1,file=atmos_profile_file_full,status='old',action='read');
DO ITEMP=1,NHEADER
  READ(1,*)
ENDDO

DO ITEMP=1,NPGRID_Atmosphere_Profile
  READ(1,*)RARRY(1:9)
  Z_FIELD_AP(ITEMP)=RARRY(1)
  PRESSURE_AP(ITEMP)=RARRY(2)*100.0D0
  T_FIELD_AP(ITEMP)=RARRY(3)
  NUMDEN_TOTAL_FIELD_AP(ITEMP)=RARRY(4)*1.0E6
  VMR_OZONE_FIELD_AP(ITEMP)=RARRY(5)*1.0E6
  VMR_OXYGEN_FIELD_AP(ITEMP)=RARRY(6)*1.0E6
  VMR_H2O_FIELD_AP(ITEMP)=RARRY(7)*1.0E6
  VMR_CO2_FIELD_AP(ITEMP)=RARRY(8)*1.0E6
  VMR_NO2_FIELD_AP(ITEMP)=RARRY(9)*1.0E6
ENDDO
CLOSE(1)

OPEN(unit=1,file=atmos_ch4_file,status='old',action='read');
DO ITEMP=1,NHEADER
  READ(1,*)
ENDDO

DO ITEMP=1,NPGRID_Atmosphere_Profile
READ(1,*)RARRY(1:2)
  VMR_CH4_FIELD_AP(ITEMP)=RARRY(2)
ENDDO
CLOSE(1)

VMR_OZONE_FIELD_AP=VMR_OZONE_FIELD_AP/NUMDEN_TOTAL_FIELD_AP
VMR_OXYGEN_FIELD_AP=VMR_OXYGEN_FIELD_AP/NUMDEN_TOTAL_FIELD_AP
VMR_H2O_FIELD_AP=VMR_H2O_FIELD_AP/NUMDEN_TOTAL_FIELD_AP
VMR_CO2_FIELD_AP=VMR_CO2_FIELD_AP/NUMDEN_TOTAL_FIELD_AP
VMR_NO2_FIELD_AP=VMR_NO2_FIELD_AP/NUMDEN_TOTAL_FIELD_AP

END SUBROUTINE Atmosphere_Profile_READIN

SUBROUTINE Surface_Pressure_Rescale(PRESSURE_SURFACE)

REAL*8,INTENT(INOUT) :: PRESSURE_SURFACE ! SURFACE PRESSURE IN mb.


REAL*8, PARAMETER :: MolarWeightDryAir=28.9647,MolarWeightH2O=18.01528, &
                     GRAVITY_ACC=9.80665

REAL*8, EXTERNAL :: POLINT_SIMPLE

INTEGER :: jlo
REAL *8 :: MolarWeightMoistAir,SCALEHEIGHT,Z_TEMP,NUMDEN_TEMP,RTMP1,RTMP2

PRESSURE_SURFACE=PRESSURE_SURFACE*100.0D0 ! CONVERT TO PASCAL

IF(PRESSURE_SURFACE>PRESSURE_AP(NPGRID_Atmosphere_Profile))THEN
   NPGRID=NPGRID_Atmosphere_Profile+1
ELSE
   jlo=NPGRID_Atmosphere_Profile-1
   call hunt(PRESSURE_AP,NPGRID_Atmosphere_Profile,PRESSURE_SURFACE,jlo)
   IF(abs(PRESSURE_AP(jlo)-PRESSURE_SURFACE)<1.0e-6) THEN
		NPGRID=jlo
   ELSE
		NPGRID=jlo+1
   ENDIF
ENDIF

ALLOCATE(P_GRID(NPGRID),Z_FIELD(NPGRID),TEM_FIELD(NPGRID),NUMDEN_TOTAL_FIELD(NPGRID), &
		 VMR_OZONE_FIELD(NPGRID),VMR_OXYGEN_FIELD(NPGRID),VMR_H2O_FIELD(NPGRID),&
		 VMR_CO2_FIELD(NPGRID),VMR_NO2_FIELD(NPGRID),VMR_CH4_FIELD(NPGRID))

P_GRID(1:NPGRID-1)=PRESSURE_AP(1:NPGRID-1)
P_GRID(NPGRID)=PRESSURE_SURFACE

IF(NPGRID>NPGRID_Atmosphere_Profile)THEN
	SCALEHEIGHT=-((Z_FIELD_AP(NPGRID_Atmosphere_Profile-1) - Z_FIELD_AP(NPGRID_Atmosphere_Profile)))    &
				/ LOG(PRESSURE_AP(NPGRID_Atmosphere_Profile-1)/ PRESSURE_AP(NPGRID_Atmosphere_Profile))
ELSE
	SCALEHEIGHT=-((Z_FIELD_AP(NPGRID-1) - Z_FIELD_AP(NPGRID)))    &
				/ LOG(PRESSURE_AP(NPGRID-1)/ PRESSURE_AP(NPGRID))
ENDIF
Z_TEMP =  Z_FIELD_AP(NPGRID-1) - SCALEHEIGHT*LOG(P_GRID(NPGRID)/P_GRID(NPGRID-1))
Z_FIELD(1:NPGRID-1)=Z_FIELD_AP(1:NPGRID-1)
Z_FIELD(NPGRID)=Z_TEMP
Z_FIELD=Z_FIELD-Z_TEMP


TEM_FIELD(1:NPGRID-1) = T_FIELD_AP(1:NPGRID-1)
IF(NPGRID>NPGRID_Atmosphere_Profile)THEN
	TEM_FIELD(NPGRID) = POLINT_SIMPLE(2,Z_FIELD_AP(NPGRID_Atmosphere_Profile-1),&
					 T_FIELD_AP(NPGRID_Atmosphere_Profile-1),Z_TEMP,2)
ELSE
    TEM_FIELD(NPGRID) = POLINT_SIMPLE(2,Z_FIELD_AP(NPGRID-1),T_FIELD_AP(NPGRID-1), &
										Z_TEMP,2)
ENDIF

VMR_OZONE_FIELD(1:NPGRID-1) = VMR_OZONE_FIELD_AP(1:NPGRID-1)
IF(NPGRID>NPGRID_Atmosphere_Profile)THEN
   VMR_OZONE_FIELD(NPGRID) = POLINT_SIMPLE(2,PRESSURE_AP(NPGRID_Atmosphere_Profile-1), &
										VMR_OZONE_FIELD_AP(NPGRID_Atmosphere_Profile-1),  &
										PRESSURE_SURFACE,2)
   IF(VMR_OZONE_FIELD(NPGRID)<0.0D0)VMR_OZONE_FIELD(NPGRID)=VMR_OZONE_FIELD_AP(NPGRID-1)
ELSE
   VMR_OZONE_FIELD(NPGRID) = POLINT_SIMPLE(2,PRESSURE_AP(NPGRID-1), &
										   VMR_OZONE_FIELD_AP(NPGRID-1),  &
										   PRESSURE_SURFACE,2)
ENDIF

VMR_OXYGEN_FIELD(1:NPGRID-1) = VMR_OXYGEN_FIELD_AP(1:NPGRID-1)
IF(NPGRID>NPGRID_Atmosphere_Profile)THEN
	VMR_OXYGEN_FIELD(NPGRID) = VMR_OXYGEN_FIELD_AP(NPGRID-1)
ELSE
	VMR_OXYGEN_FIELD(NPGRID) = POLINT_SIMPLE(2,PRESSURE_AP(NPGRID-1),         &
											   VMR_OXYGEN_FIELD_AP(NPGRID-1),  &
											   PRESSURE_SURFACE,2)
ENDIF

VMR_H2O_FIELD(1:NPGRID-1) = VMR_H2O_FIELD_AP(1:NPGRID-1)
IF(NPGRID>NPGRID_Atmosphere_Profile)THEN
    VMR_H2O_FIELD(NPGRID) = POLINT_SIMPLE(2,PRESSURE_AP(NPGRID_Atmosphere_Profile-1),&
									VMR_H2O_FIELD_AP(NPGRID_Atmosphere_Profile-1),   &
									PRESSURE_SURFACE,2)
ELSE
	VMR_H2O_FIELD(NPGRID) = POLINT_SIMPLE(2,PRESSURE_AP(NPGRID-1),         &
							            VMR_H2O_FIELD_AP(NPGRID-1),  &
										PRESSURE_SURFACE,2)
ENDIF

VMR_CO2_FIELD(1:NPGRID-1) = VMR_CO2_FIELD_AP(1:NPGRID-1)
IF(NPGRID>NPGRID_Atmosphere_Profile)THEN
	VMR_CO2_FIELD(NPGRID) = VMR_CO2_FIELD_AP(NPGRID-1)
ELSE
	VMR_CO2_FIELD(NPGRID) = POLINT_SIMPLE(2,PRESSURE_AP(NPGRID-1),         &
									    VMR_CO2_FIELD_AP(NPGRID-1),  &
									    PRESSURE_SURFACE,2)
ENDIF

VMR_NO2_FIELD(1:NPGRID-1) = VMR_NO2_FIELD_AP(1:NPGRID-1)
IF(NPGRID>NPGRID_Atmosphere_Profile)THEN
	VMR_NO2_FIELD(NPGRID) = VMR_NO2_FIELD_AP(NPGRID-1)
ELSE
	VMR_NO2_FIELD(NPGRID) = POLINT_SIMPLE(2,PRESSURE_AP(NPGRID-1),   &
			                            VMR_NO2_FIELD_AP(NPGRID-1),  &
										PRESSURE_SURFACE,2)
ENDIF

VMR_CH4_FIELD(1:NPGRID-1) = VMR_CH4_FIELD_AP(1:NPGRID-1)
IF(NPGRID>NPGRID_Atmosphere_Profile)THEN
	VMR_CH4_FIELD(NPGRID) = VMR_CH4_FIELD_AP(NPGRID-1)
ELSE
	VMR_CH4_FIELD(NPGRID) = POLINT_SIMPLE(2,PRESSURE_AP(NPGRID-1),   &
						                VMR_CH4_FIELD_AP(NPGRID-1),  &
									    PRESSURE_SURFACE,2)
ENDIF

IF(NPGRID>NPGRID_Atmosphere_Profile)THEN
	MolarWeightMoistAir=(1.0-VMR_H2O_FIELD(NPGRID))*MolarWeightDryAir  &
						   + VMR_H2O_FIELD(NPGRID)*MolarWeightH2O
	RTMP1=P_GRID(NPGRID)-P_GRID(NPGRID-1)
	RTMP2=1000.0D0*RTMP1*AvogadroN/GRAVITY_ACC/MolarWeightMoistAir  ! 1000 is g to kg.

	RTMP2=RTMP2/SCALEHEIGHT/1000d0
	NUMDEN_TOTAL_FIELD(NPGRID) = RTMP2+NUMDEN_TOTAL_FIELD_AP(NPGRID-1)
ELSE
	NUMDEN_TOTAL_FIELD(NPGRID) = POLINT_SIMPLE(2,PRESSURE_AP(NPGRID-1),   &
	                                    NUMDEN_TOTAL_FIELD_AP(NPGRID-1),  &
             							PRESSURE_SURFACE,2)
ENDIF

NUMDEN_TOTAL_FIELD(1:NPGRID-1) = NUMDEN_TOTAL_FIELD_AP(1:NPGRID-1)


!write(*,*)'Z_FIELD, P_GRID,TEM_FIELD,NUMDEN_TOTAL_FIELD'
!DO JLO=1,NPGRID
! WRITE(*,*)Z_FIELD(JLO), P_GRID(JLO),TEM_FIELD(JLO),NUMDEN_TOTAL_FIELD(JLO),&
!            NUMDEN_TOTAL_FIELD(JLO)*VMR_OZONE_FIELD(JLO),  &
!			NUMDEN_TOTAL_FIELD(JLO)*VMR_Oxygen_FIELD(JLO),  &
!			NUMDEN_TOTAL_FIELD(JLO)*VMR_H2O_FIELD(JLO),  &
!			NUMDEN_TOTAL_FIELD(JLO)*VMR_CO2_FIELD(JLO),  &
!			NUMDEN_TOTAL_FIELD(JLO)*VMR_NO2_FIELD(JLO)
!ENDDO

END SUBROUTINE Surface_Pressure_Rescale

SUBROUTINE DEALLO_ATMOSPRF
DEALLOCATE(P_GRID,Z_FIELD,TEM_FIELD,NUMDEN_TOTAL_FIELD,VMR_OZONE_FIELD, &
			VMR_OXYGEN_FIELD,VMR_H2O_FIELD,VMR_CO2_FIELD,VMR_NO2_FIELD,VMR_CH4_FIELD)
ENDSUBROUTINE DEALLO_ATMOSPRF

SUBROUTINE GAS_COLUMN_RESCALE(OZONE_COLUMN,H2O_COLUMN)
REAL*8,INTENT(IN) :: OZONE_COLUMN ! OZONE IN THE WHOLE COLUMN IN DOBSON UNIT
REAL*8,INTENT(IN) :: H2O_COLUMN   ! WATER VAPOR IN THE WHOLE COLUMN IN CENTIMETERS.

REAL*8, PARAMETER :: Dobson=2.6867E20   !molecules per meter square, equivalent to 10^{-5} meter in one STP
!REAL*8, PARAMETER :: Num_Mol_atm_cm=Dobson*1000     ! 1 cm = 10^{-2} meter

REAL*8, PARAMETER :: Density_liquid_water = 1000.0d0 ! kg/m3

REAL*8,DIMENSION(:),ALLOCATABLE :: NUMDEN_FIELD_TEMP,Z_FIELD_TEMP

REAL*8,EXTERNAL :: TRAPZOID


INTEGER :: IPGRID
REAL*8 :: RTMP1,RTMP2

ALLOCATE(NUMDEN_FIELD_TEMP(NPGRID),Z_FIELD_TEMP(NPGRID))

Z_FIELD_TEMP=Z_FIELD*1000.0D0  ! CONVERT ATMOSPHERE ALTITUDE FROM KM TO METERS

! Calculate total column ozone in Dobson Unit
NUMDEN_FIELD_TEMP=NUMDEN_TOTAL_FIELD*VMR_OZONE_FIELD
RTMP1=TRAPZOID(NPGRID,Z_FIELD_TEMP,NUMDEN_FIELD_TEMP)
OZONE_STD_COLUMN=ABS(RTMP1)/Dobson

!rescale ozone profile with input column ozone
NUMDEN_FIELD_TEMP=NUMDEN_FIELD_TEMP/OZONE_STD_COLUMN*OZONE_COLUMN
VMR_OZONE_FIELD=NUMDEN_FIELD_TEMP/NUMDEN_TOTAL_FIELD

!CALCULATE TOTAL COLUMN WATER VAPOR IN WATER FORMAT (Centimeters)
NUMDEN_FIELD_TEMP=NUMDEN_TOTAL_FIELD*VMR_H2O_FIELD
NUMDEN_FIELD_TEMP=NUMDEN_FIELD_TEMP/AvogadroN/1000.0d0*MolarWeight_H2O ! Mass profile in kg.
RTMP2=TRAPZOID(NPGRID,Z_FIELD_TEMP,NUMDEN_FIELD_TEMP) !Mass in column
H2O_STD_COLUMN=ABS(RTMP2)/Density_liquid_water*100.0d0        ! Column Water depth in cm

! Rescale water vapor profile using input values
NUMDEN_FIELD_TEMP=NUMDEN_TOTAL_FIELD*VMR_H2O_FIELD
NUMDEN_FIELD_TEMP=NUMDEN_FIELD_TEMP/H2O_STD_COLUMN*H2O_COLUMN
VMR_H2O_FIELD=NUMDEN_FIELD_TEMP/NUMDEN_TOTAL_FIELD

!write(*,*)'total column ozone in dobson unit:',RTMP1
!write(*,*)'total column H2O in CM:',RTMP2

DEALLOCATE(NUMDEN_FIELD_TEMP,Z_FIELD_TEMP)

END SUBROUTINE GAS_COLUMN_RESCALE


SUBROUTINE NO2_COLUMN_RESCALE(NO2_COLUMN)
REAL*8,INTENT(IN) :: NO2_COLUMN ! NO2 IN THE WHOLE COLUMN IN DOBSON UNIT

REAL*8, PARAMETER :: Dobson=2.6867E20   !molecules per meter square, equivalent to 10^{-5} meter in one STP
!REAL*8, PARAMETER :: Num_Mol_atm_cm=Dobson*1000     ! 1 cm = 10^{-2} meter

REAL*8,DIMENSION(:),ALLOCATABLE :: NUMDEN_FIELD_TEMP,Z_FIELD_TEMP

REAL*8,EXTERNAL :: TRAPZOID

INTEGER :: IPGRID
REAL*8 :: RTMP1,RTMP2

ALLOCATE(NUMDEN_FIELD_TEMP(NPGRID),Z_FIELD_TEMP(NPGRID))

Z_FIELD_TEMP=Z_FIELD*1000.0D0  ! CONVERT ATMOSPHERE ALTITUDE FROM KM TO METERS

! Calculate total column no2 in Dobson Unit
NUMDEN_FIELD_TEMP=NUMDEN_TOTAL_FIELD*VMR_NO2_FIELD
RTMP1=TRAPZOID(NPGRID,Z_FIELD_TEMP,NUMDEN_FIELD_TEMP)
NO2_STD_COLUMN=ABS(RTMP1)/Dobson

!rescale no2 profile with input column no2
NUMDEN_FIELD_TEMP=NUMDEN_FIELD_TEMP/NO2_STD_COLUMN*NO2_COLUMN
VMR_NO2_FIELD=NUMDEN_FIELD_TEMP/NUMDEN_TOTAL_FIELD

!write(*,*)'total column no2 in dobson unit:',RTMP1

DEALLOCATE(NUMDEN_FIELD_TEMP,Z_FIELD_TEMP)

END SUBROUTINE NO2_COLUMN_RESCALE

END MODULE Atmosphere_Profile
