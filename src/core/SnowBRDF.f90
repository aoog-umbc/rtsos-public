MODULE SnowBRDF
USE RTTYPE, ONLY : PI

IMPLICIT NONE
LOGICAL :: fSnowBRDF=.FALSE.

CHARACTER*360 ::snow_brdf_path

INTEGER,PARAMETER :: NWave_SnowBRDF_nm=83,NThetaV_ABCD=6,NPhiV_Snow=48,&
                     NThetaV_E=4,NThetaV_F=3,NThetaV_GRID=8,NPhiV_GRID=49

DOUBLE PRECISION,DIMENSION(NWave_SnowBRDF_nm) :: Wave_SnowBRDF_nm,SNOW_ALBEDO
DOUBLE PRECISION,DIMENSION(NThetaV_ABCD) :: ThetaV_ABCD
DOUBLE PRECISION,DIMENSION(NThetaV_E) :: ThetaV_E
DOUBLE PRECISION,DIMENSION(NThetaV_F) :: ThetaV_F
DOUBLE PRECISION,DIMENSION(NPhiV_Snow) :: PhiV_Snow

DOUBLE PRECISION,DIMENSION(NThetaV_GRID) :: ThetaV_GRID
DOUBLE PRECISION,DIMENSION(NPhiV_GRID) :: PhiV_GRID
DOUBLE PRECISION,DIMENSION(NThetaV_GRID,NPhiV_GRID) :: SnowBRDF_GRID

DOUBLE PRECISION,DIMENSION(NThetaV_ABCD,NPhiV_Snow) :: EOF1_A,EOF2_A,EOF1_B,EOF2_B,&
            EOF1_C,EOF2_C,EOF1_D,EOF2_D,EOF3_D
DOUBLE PRECISION,DIMENSION(NThetaV_E,NPhiV_Snow) :: EOF1_E
DOUBLE PRECISION,DIMENSION(NThetaV_F,NPhiV_Snow) :: EOF1_F,EOF2_F

CONTAINS
SUBROUTINE SNOW_BRDF_READIN

CHARACTER*360 :: filename_readin

INTEGER,PARAMETER :: NHEADER=1

INTEGER :: ITEMP,ITEMP1

! Read in albedo
filename_readin=trim(snow_brdf_path)//'/albedo.txt'
OPEN(unit=1,file=filename_readin,status='old',action='read');
DO ITEMP=1,NHEADER
  READ(1,*)
ENDDO

DO ITEMP=1,NWave_SnowBRDF_nm
  READ(1,*)Wave_SnowBRDF_nm(ITEMP),SNOW_ALBEDO(ITEMP)
ENDDO
CLOSE(1)

! Read in EOF_A
filename_readin=trim(snow_brdf_path)//'/eof_A.txt'
OPEN(unit=1,file=filename_readin,status='old',action='read');
DO ITEMP=1,NHEADER
  READ(1,*)
ENDDO

DO ITEMP=1,NThetaV_ABCD
DO ITEMP1=1,NPhiV_Snow
  READ(1,*)ThetaV_ABCD(ITEMP),PhiV_Snow(ITEMP1),EOF1_A(ITEMP,ITEMP1),EOF2_A(ITEMP,ITEMP1)
ENDDO
ENDDO
CLOSE(1)

! Read in EOF_B
filename_readin=trim(snow_brdf_path)//'/eof_B.txt'
OPEN(unit=1,file=filename_readin,status='old',action='read');
DO ITEMP=1,NHEADER
  READ(1,*)
ENDDO

DO ITEMP=1,NThetaV_ABCD
DO ITEMP1=1,NPhiV_Snow
  READ(1,*)ThetaV_ABCD(ITEMP),PhiV_Snow(ITEMP1),EOF1_B(ITEMP,ITEMP1),EOF2_B(ITEMP,ITEMP1)
ENDDO
ENDDO
CLOSE(1)

! Read in EOF_C
filename_readin=trim(snow_brdf_path)//'/eof_C.txt'
OPEN(unit=1,file=filename_readin,status='old',action='read');
DO ITEMP=1,NHEADER
  READ(1,*)
ENDDO

DO ITEMP=1,NThetaV_ABCD
DO ITEMP1=1,NPhiV_Snow
  READ(1,*)ThetaV_ABCD(ITEMP),PhiV_Snow(ITEMP1),EOF1_C(ITEMP,ITEMP1),EOF2_C(ITEMP,ITEMP1)
ENDDO
ENDDO
CLOSE(1)

! Read in EOF_D
filename_readin=trim(snow_brdf_path)//'/eof_D.txt'
OPEN(unit=1,file=filename_readin,status='old',action='read');
DO ITEMP=1,NHEADER
  READ(1,*)
ENDDO

DO ITEMP=1,NThetaV_ABCD
DO ITEMP1=1,NPhiV_Snow
  READ(1,*)ThetaV_ABCD(ITEMP),PhiV_Snow(ITEMP1),EOF1_D(ITEMP,ITEMP1), &
          EOF2_D(ITEMP,ITEMP1),EOF3_D(ITEMP,ITEMP1)
ENDDO
ENDDO
CLOSE(1)


! Read in EOF_E
filename_readin=trim(snow_brdf_path)//'/eof_E.txt'
OPEN(unit=1,file=filename_readin,status='old',action='read');
DO ITEMP=1,NHEADER
  READ(1,*)
ENDDO

DO ITEMP=1,NThetaV_E
DO ITEMP1=1,NPhiV_Snow
  READ(1,*)ThetaV_E(ITEMP),PhiV_Snow(ITEMP1),EOF1_E(ITEMP,ITEMP1)
ENDDO
ENDDO
CLOSE(1)

! Read in EOF_F
filename_readin=trim(snow_brdf_path)//'/eof_F.txt'
OPEN(unit=1,file=filename_readin,status='old',action='read');
DO ITEMP=1,NHEADER
  READ(1,*)
ENDDO

DO ITEMP=1,NThetaV_F
DO ITEMP1=1,NPhiV_Snow
  READ(1,*)ThetaV_F(ITEMP),PhiV_Snow(ITEMP1),EOF1_F(ITEMP,ITEMP1),EOF2_F(ITEMP,ITEMP1)
ENDDO
ENDDO
CLOSE(1)

! transform Hudson et al. viewing azimuth to the viewing azimuth convention used in RTSOS
ThetaV_GRID(1)=0.0D0
ThetaV_GRID(2:NThetaV_ABCD+1)=ThetaV_ABCD(1:NThetaV_ABCD)
ThetaV_GRID(NThetaV_ABCD+2)=90.0D0

PhiV_GRID(1:NPhiV_Snow)=360d0-PhiV_Snow(1:NPhiV_Snow)
PhiV_GRID(NPhiV_Snow+1)=0.0D0

PhiV_GRID=PI*PhiV_GRID/180.D0
ThetaV_GRID=PI*ThetaV_GRID/180.D0

END SUBROUTINE SNOW_BRDF_READIN

SUBROUTINE SNOW_BRDF_V_CAL(MU0,LAMBDA_NM,CASESTRING,V_VAL,EIGEN_VAL,ALBEDO_LOCAL)
DOUBLE PRECISION, INTENT(IN) :: MU0,LAMBDA_NM
CHARACTER,INTENT(IN) :: CASESTRING
DOUBLE PRECISION,DIMENSION(3),INTENT(OUT) ::V_VAL,EIGEN_VAL
DOUBLE PRECISION,INTENT(in) ::ALBEDO_LOCAL

DOUBLE PRECISION :: ABSMU0,MU_M_WV,LAMBDA_MICRON,MU_M_ALBEDO
LAMBDA_MICRON=LAMBDA_NM/1000.0D0
ABSMU0=ABS(MU0)
MU_M_WV=ABSMU0*LAMBDA_MICRON

MU_M_ALBEDO=ABSMU0*ALBEDO_LOCAL

V_VAL=0.0D0
EIGEN_VAL=0.0D0
IF(CASESTRING == 'A') THEN
V_VAL(1)=-0.0258D0 + 1.34D0 * ABSMU0**10.1D0 + 0.181D0 * LAMBDA_MICRON**0.519D0 - 0.206D0 * MU_M_WV**0.608D0
V_VAL(2)=0.105D0 - 0.0365D0 * ABSMU0**(-1.01D0) - 0.0073D0 * LAMBDA_MICRON**(-1.63D0) + 4.94D-5 * MU_M_WV**(-2.86D0)
EIGEN_VAL(1)=75.6D0
EIGEN_VAL(2)=24.2D0
ENDIF

IF(CASESTRING == 'B') THEN
V_VAL(1)=-0.784D0 + 1.35D0 * ABSMU0**(-0.0872D0) - 0.399D0 * LAMBDA_MICRON**0.251D0 - 0.213D0 * MU_M_WV**(-0.284D0)
V_VAL(2)=1.9D0 + 1.39D0 * ABSMU0**(1.32) - 0.0671D0 * LAMBDA_MICRON**(-1.11D0) - 2.43D0 * MU_M_WV**0.101D0
EIGEN_VAL(1)=217D0
EIGEN_VAL(2)=17.9D0
ENDIF

IF(CASESTRING == 'C') THEN
V_VAL(1)=-0.0623D0 - 7.98D-5 * ALBEDO_LOCAL**(-5.99D0) + 0.0519D0 * MU_M_ALBEDO**(-0.454D0)
V_VAL(2)=0.0902D0 - 0.0349D0 * ABSMU0**(-1.07D0)
EIGEN_VAL(1)=121D0
EIGEN_VAL(2)=23.9D0
ENDIF

IF(CASESTRING == 'D') THEN
V_VAL(1)=-0.195D0 - 3.72D-4 * ABSMU0**(-1.67D0) + 0.0569D0 * ALBEDO_LOCAL**(0.369D0) + 0.118D0 * MU_M_ALBEDO**(-0.216D0)
V_VAL(2)=1.75D0 - 0.132D0 * ABSMU0**(-0.592D0) + 0.179D0 * ALBEDO_LOCAL**(3.11D0) - 1.91D0 * MU_M_ALBEDO**(0.146D0)
V_VAL(3)=0.644D0 + 1.92D0 * ABSMU0**(1.12D0) - 0.0221D0 * ALBEDO_LOCAL**(-2.82D0) - 1.61D0 * MU_M_ALBEDO**(0.298D0)

EIGEN_VAL(1)=289D0
EIGEN_VAL(2)=21.8D0
EIGEN_VAL(3)=16.8D0
ENDIF

IF(CASESTRING == 'E') THEN
V_VAL(1)=0.106D0 - 0.124D0 * MU_M_ALBEDO**(0.193D0)
EIGEN_VAL(1)=129D0
ENDIF

IF(CASESTRING == 'F') THEN
V_VAL(1)=0.0161D0 - 0.0103D0 * MU_M_ALBEDO**(-0.599D0)
V_VAL(2)=-0.151D0 + 0.0850D0 * ABSMU0**(-0.679D0)

EIGEN_VAL(1)=301D0
EIGEN_VAL(2)=36.8D0
ENDIF

END SUBROUTINE SNOW_BRDF_V_CAL

SUBROUTINE SNOW_BRDF_GRID_CAL(MU0,LAMBDA_NM,ALBEDO_LOCAL)

DOUBLE PRECISION, INTENT(IN) :: MU0,LAMBDA_NM
DOUBLE PRECISION,DIMENSION(3) ::V_VAL,EIGEN_VAL
DOUBLE PRECISION,INTENT(OUT) :: ALBEDO_LOCAL

DOUBLE PRECISION, EXTERNAL :: Func_UVIP3P

DOUBLE PRECISION :: ABSMU0,THETA0,LAMBDA_NM_LOCAL

CHARACTER :: CASESTRING,CASESTRING1

INTEGER :: ITEMP,ITEMP1

!CALL SNOW_BRDF_READIN

ABSMU0=DABS(MU0)
THETA0=ACOS(ABSMU0)*180D0/PI
SnowBRDF_GRID=0.0d0

ALBEDO_LOCAL =Func_UVIP3P(NWave_SnowBRDF_nm,Wave_SnowBRDF_nm,SNOW_ALBEDO,LAMBDA_NM,2)

IF(THETA0<51.6D0)THEN
! ASSUMING LAMBERTIAN REFLECTANCE FOR SOLAR ZENITH ANGLE SMALLER THAN 51.6 DEGREES
   SnowBRDF_GRID=1.0D0
   RETURN
ENDIF

LAMBDA_NM_LOCAL=LAMBDA_NM
IF(LAMBDA_NM<350D0)THEN
   LAMBDA_NM_LOCAL=350D0
ELSEIF(LAMBDA_NM>2400D0)THEN
   LAMBDA_NM_LOCAL=2400D0
ENDIF

! ASSUMING SOLAR ZENITH ANGLE LARGER THAN 86.6 DEGREES ASSUMED TO BE THE SAME AS THOSE OF 86.6D0 DEGREES.
IF(LAMBDA_NM<=1400D0 .AND. THETA0>86.6D0 ) THETA0=86.6D0
IF(LAMBDA_NM>1400D0 .AND. THETA0>75.D0 ) THETA0=75.D0

ABSMU0=DCOS(THETA0/180.0d0*PI)

IF(LAMBDA_NM_LOCAL>=350D0 .AND. LAMBDA_NM_LOCAL<=950D0)THEN
    IF(THETA0>=51.6D0 .AND. THETA0<=75D0) THEN
          CASESTRING='A'
    ELSEIF(THETA0>75D0 .AND. THETA0<=86.6D0) THEN
          CASESTRING='B'
    ENDIF
ELSEIF(LAMBDA_NM_LOCAL>950D0 .AND. LAMBDA_NM_LOCAL<=1400D0)THEN
    IF(THETA0>=51.6D0 .AND. THETA0<=75D0) THEN
          CASESTRING='C'
    ELSEIF(THETA0>75D0 .AND. THETA0<=86.6D0) THEN
          CASESTRING='D'
    ENDIF
ELSEIF(LAMBDA_NM_LOCAL>1400D0 .AND. LAMBDA_NM_LOCAL<=2400D0)THEN
          CASESTRING='E'
ENDIF

CALL SNOW_BRDF_V_CAL(ABSMU0,LAMBDA_NM_LOCAL,CASESTRING,V_VAL,EIGEN_VAL,ALBEDO_LOCAL)

DO ITEMP=1,NThetaV_ABCD
DO ITEMP1=1,NPhiV_Snow

  IF(CASESTRING=='A')THEN
      SnowBRDF_GRID(ITEMP+1,ITEMP1)=EIGEN_VAL(1)*V_VAL(1)*EOF1_A(ITEMP,ITEMP1) + &
									EIGEN_VAL(2)*V_VAL(2)*EOF2_A(ITEMP,ITEMP1)
  ELSEIF(CASESTRING=='B')THEN
      SnowBRDF_GRID(ITEMP+1,ITEMP1)=EIGEN_VAL(1)*V_VAL(1)*EOF1_B(ITEMP,ITEMP1) + &
									EIGEN_VAL(2)*V_VAL(2)*EOF2_B(ITEMP,ITEMP1)


ELSEIF(CASESTRING=='C')THEN
	  SnowBRDF_GRID(ITEMP+1,ITEMP1)=EIGEN_VAL(1)*V_VAL(1)*EOF1_C(ITEMP,ITEMP1) + &
									EIGEN_VAL(2)*V_VAL(2)*EOF2_C(ITEMP,ITEMP1)
  ELSEIF(CASESTRING=='D')THEN
	  SnowBRDF_GRID(ITEMP+1,ITEMP1)=EIGEN_VAL(1)*V_VAL(1)*EOF1_D(ITEMP,ITEMP1) + &
		  						    EIGEN_VAL(2)*V_VAL(2)*EOF2_D(ITEMP,ITEMP1) + &
									EIGEN_VAL(3)*V_VAL(3)*EOF3_D(ITEMP,ITEMP1)
  ELSEIF(CASESTRING=='E' .AND. ThetaV_ABCD(ITEMP) <= ThetaV_E(NThetaV_E))THEN
	  SnowBRDF_GRID(ITEMP+1,ITEMP1)=EIGEN_VAL(1)*V_VAL(1)*EOF1_E(ITEMP,ITEMP1)
  ENDIF

ENDDO
ENDDO

IF(CASESTRING=='E')THEN
  CASESTRING1='F'
  CALL SNOW_BRDF_V_CAL(ABSMU0,LAMBDA_NM_LOCAL,CASESTRING1,V_VAL,EIGEN_VAL,ALBEDO_LOCAL)
  DO ITEMP=1,NThetaV_ABCD
  DO ITEMP1=1,NPhiV_Snow
     IF(ThetaV_ABCD(ITEMP) > ThetaV_F(1))THEN
	    SnowBRDF_GRID(ITEMP+1,ITEMP1)=EIGEN_VAL(1)*V_VAL(1)*EOF1_F(ITEMP-NThetaV_E+1,ITEMP1) + &
                                      EIGEN_VAL(2)*V_VAL(2)*EOF2_F(ITEMP-NThetaV_E+1,ITEMP1)
     ENDIF

  ENDDO
  ENDDO
ENDIF

SnowBRDF_GRID(2:NThetaV_ABCD+1,NPhiV_Snow+1)=SnowBRDF_GRID(2:NThetaV_ABCD+1,1)
SnowBRDF_GRID(1,:)=SnowBRDF_GRID(2,:)
SnowBRDF_GRID(NThetaV_ABCD+2,:)=SnowBRDF_GRID(NThetaV_ABCD+1,:)
SnowBRDF_GRID=1.0D0+SnowBRDF_GRID

END SUBROUTINE SNOW_BRDF_GRID_CAL

SUBROUTINE pBRDM_Snow(WAVELENGTH_MICRON,COST1,PHI1,COST2,PHI2,BRDML,GLBDO)
IMPLICIT none
DOUBLE PRECISION,INTENT(IN) :: WAVELENGTH_MICRON,COST1,PHI1,COST2,PHI2,GLBDO
DOUBLE PRECISION,DIMENSION(4,4),INTENT(OUT) :: BRDML

DOUBLE PRECISION :: LAMBDA_NM,SINT1,SINT2,THETA1,THETA2, PHIV ,RTMP,ALBEDO_LOCAL,dy
!write(*,*)'WAVELENGTH_MICRON=',WAVELENGTH_MICRON
INTEGER :: ITEMP,ITEMP1
LOGICAL :: USER_GLBDO_INPUT=.True.

LAMBDA_NM=WAVELENGTH_MICRON*1000.0D0

IF(LAMBDA_NM<300D0 .OR. LAMBDA_NM>2500D0 ) THEN
  WRITE(*,*) 'LAMBDA_NM=',LAMBDA_NM
  STOP 'LAMBDA_NM VALUE OUT OF BOUND'
ENDIF


SINT1=DSQRT(1.0D0-COST1*COST1)
SINT2=DSQRT(1.0D0-COST2*COST2)
PHIV=PHI2-PHI1
IF(PHIV<0.0D0)PHIV=PHIV+2.0D0*PI
IF(PHIV>2.0D0*PI)PHIV=PHIV-2.0D0*PI

THETA1=ASIN(SINT1)
THETA2=ASIN(SINT2)

CALL SNOW_BRDF_GRID_CAL(COST1,LAMBDA_NM,ALBEDO_LOCAL)
call interpl2d(ThetaV_GRID,PhiV_GRID,SnowBRDF_GRID,NThetaV_GRID,NPhiV_GRID,THETA2,PHIV,RTMP,dy)

BRDML=0.0d0
IF(USER_GLBDO_INPUT)THEN
   BRDML(1,1)=RTMP*GLBDO*DABS(COST1)       ! use user input GLBDO value
ELSE
   BRDML(1,1)=RTMP*ALBEDO_LOCAL*DABS(COST1) ! use database value
ENDIF
!BRDML(1,1)=GLBDO*DABS(COST1) ! debugging line. This should generate the same result as lambertian
if(abs(rtmp)>40)then
  WRITE(*,*)'MIN(PhiV_GRID),MAX(PhiV_GRID)=',MINVAL(PhiV_GRID),MAXVAL(PhiV_GRID)
  WRITE(*,*)LAMBDA_NM,THETA1/PI*180,THETA2/PI*180,PHIV/PI*180,RTMP
  WRITE(*,*)'MIN(SnowBRDF_GRID),MAX(SnowBRDF_GRID)=',MINVAL(SnowBRDF_GRID),MAXVAL(SnowBRDF_GRID)
  stop
endif
END SUBROUTINE pBRDM_Snow

END MODULE SnowBRDF
