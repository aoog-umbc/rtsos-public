SUBROUTINE DELTAFIT
USE RTUTILITY, ONLY : PI,MAXLORD,NUMMIEANG,MU_SNG,PM_AO_SNG,NUMMIE, FTRUNC, ANGLE_TRUNCATION,&
               BETAL,ALPHAL,ZETAL,DELTAL,GAMMAL,EPSILONL,DELTAM,RAMAN_SOURCE_FLAG
USE SAVE_OCEAN_MOD,ONLY: WRAMANBETAL,WRAMANALPHAL,WRAMANZETAL,WRAMANDELTAL,WRAMANGAMMAL,&
                WRAMANEPSILONL,WRAMANDEPOL

IMPLICIT NONE

real*8,DIMENSION(:),ALLOCATABLE :: DL00,DL20,DL2P2,DL2N2

INTEGER :: NUMLORD,IANG,L,NUMANG,IMIE
real*8 :: ANGCUT,MUF,RTMP,COEFF1,COEFF2,COEFF3

! DELTA FIT DIFINITION
real*8,  DIMENSION(:),ALLOCATABLE :: ANG,P11FIT,P11,P12,P22,P33,P34,P44
real*8,DIMENSION(:),ALLOCATABLE :: BETAL_LOCAL,ALPHAL_LOCAL,ZETAL_LOCAL,DELTAL_LOCAL,&
                                   GAMMAL_LOCAL,EPSILONL_LOCAL,TMPL

real*8 :: FTRUNCFIT

LOGICAL :: OUTPUTTST,RAMAN_SOURCE_FLAG_LOCAL

INTERFACE
SUBROUTINE GETDMLlocal(XJ,NUMLORD,DL00,DL20,DL2P2,DL2N2)
IMPLICIT NONE

real*8, INTENT(IN) ::XJ
INTEGER, INTENT(IN) ::NUMLORD
real*8,INTENT(OUT),DIMENSION(0:NUMLORD)::DL00,DL20,DL2P2,DL2N2
END SUBROUTINE GETDMLlocal
END INTERFACE

OUTPUTTST=.true.     ! IF TRUE, WRITE INPUT AND OUTPUT FILE FOR COMPARISON
NUMLORD=MAXLORD       ! should be less than 300
!RAMAN_SOURCE_FLAG_LOCAL=RAMAN_SOURCE_FLAG
RAMAN_SOURCE_FLAG_LOCAL=.FALSE.
  ! FIXED RAMAN EXPANSION COEFFICIENT IN Save_Ocean_Rad.f90, 
  ! KEPT HERE FOR FUTURE TESTING

IF(RAMAN_SOURCE_FLAG_LOCAL .AND. NUMLORD<2) &
         STOP 'NUMLORD SHOULD BE GREATER THAN 2 FOR RAMAN CALCULATION'

ALLOCATE(DL00(0:NUMLORD),DL20(0:NUMLORD),DL2P2(0:NUMLORD),  &
           DL2N2(0:NUMLORD))
ALLOCATE(BETAL_LOCAL(0:NUMLORD),ALPHAL_LOCAL(0:NUMLORD),ZETAL_LOCAL(0:NUMLORD),    &
         DELTAL_LOCAL(0:NUMLORD),GAMMAL_LOCAL(0:NUMLORD),EPSILONL_LOCAL(0:NUMLORD),&
		 TMPL(0:NUMLORD)  )

DO IMIE=1,NUMMIE+1

  IF(IMIE==NUMMIE+1 .AND. .NOT.RAMAN_SOURCE_FLAG_LOCAL)CYCLE

  IF(IMIE==NUMMIE+1)THEN
     NUMANG=NUMMIEANG(1)
  ELSE
     NUMANG=NUMMIEANG(IMIE)
  ENDIF

  ALLOCATE(ANG(NUMANG),P11FIT(NUMANG), P11(NUMANG), &
        P12(NUMANG),P22(NUMANG),P33(NUMANG),P34(NUMANG),P44(NUMANG))
  P11=0.0D0

!CALL ANGASSIGN(NUMANG,ANG)
  IF(IMIE==NUMMIE+1 .AND. RAMAN_SOURCE_FLAG_LOCAL)THEN
    NUMLORD=2
    COEFF3=(1.0D0+2.0D0*WRAMANDEPOL)
    COEFF1=3.0D0*WRAMANDEPOL/COEFF3
    COEFF2=0.75d0*(1.0D0-WRAMANDEPOL)/COEFF3
    COEFF3=0.75d0*(2.0d0-6.0d0*WRAMANDEPOL)/COEFF3
    DO IANG=1,NUMMIEANG(1)
       ANG(IANG)=ACOS(MU_SNG(1,IANG))/PI*180.0d0 !borrow the angular grid from other sets.
       RTMP=MU_SNG(1,IANG)*MU_SNG(1,IANG)
       P11FIT(IANG)= COEFF1+COEFF2*(RTMP+1.0d0)
       P12(IANG)=COEFF2*(RTMP-1.0d0)
       P22(IANG)=COEFF2*(RTMP+1.0d0)
       P33(IANG)=2.0d0*COEFF2*MU_SNG(1,IANG)
       P34(IANG)=0.0D0
       P44(IANG)=COEFF3*MU_SNG(1,IANG)
    ENDDO
  ELSE
    DO IANG=1,NUMMIEANG(IMIE)
       ANG(IANG)=ACOS(MU_SNG(IMIE,IANG))/PI*180.0d0
       P11FIT(IANG)=PM_AO_SNG(IMIE,IANG)%PHMX(1,1)
       P12(IANG)=PM_AO_SNG(IMIE,IANG)%PHMX(1,2)
       P22(IANG)=PM_AO_SNG(IMIE,IANG)%PHMX(2,2)
       P33(IANG)=PM_AO_SNG(IMIE,IANG)%PHMX(3,3)
       P34(IANG)=PM_AO_SNG(IMIE,IANG)%PHMX(3,4)
       P44(IANG)=PM_AO_SNG(IMIE,IANG)%PHMX(4,4)
       RTMP=1.0D0+(P22(IANG)/P11FIT(IANG))**2 +   (P33(IANG)/P11FIT(IANG))**2 &
                 +(P44(IANG)/P11FIT(IANG))**2 + 2*(P12(IANG)/P11FIT(IANG))**2 &
               +2*(P34(IANG)/P11FIT(IANG))**2
       IF(RTMP-4.0D0>1.0D-4) THEN
          WRITE(*,*)'SUM(FIJ^2)=',RTMP
          WRITE(*,*)'Warning, CHECK PHASE MATRIX INPUT, BASED ON FRY & KATTAWAR AO,1981'
       ENDIF
       IF(ABS(P22(IANG)/P11FIT(IANG))>1.0D0+1.0D-4) THEN
          WRITE(*,*)'P22(IANG)/P11FIT(IANG)=',P22(IANG)/P11FIT(IANG)
          P22(IANG)=sign(P11FIT(IANG),P22(IANG))
!          STOP 'CHECK P22/P11'
       ENDIF

       IF(ABS(P33(IANG)/P11FIT(IANG))>1.0D0+1.0D-4)THEN
           WRITE(*,*)'P33(IANG)/P11FIT(IANG)=',P33(IANG)/P11FIT(IANG)
           P33(IANG)=sign(P11FIT(IANG),P33(IANG))
!           STOP 'CHECK P33/P11'
       ENDIF

       IF(ABS(P44(IANG)/P11FIT(IANG))>1.0D0+1.0D-4)THEN
            WRITE(*,*)'P44(IANG)/P11FIT(IANG)=',P44(IANG)/P11FIT(IANG)
            P44(IANG)=sign(P11FIT(IANG),P44(IANG))
!            STOP 'CHECK P44/P11'
       ENDIF
       IF(ABS(P12(IANG)/P11FIT(IANG))>1.0D0+1.0D-4)THEN
            WRITE(*,*)'P12(IANG)/P11FIT(IANG)=',P12(IANG)/P11FIT(IANG)
            P12(IANG)=sign(P11FIT(IANG),P12(IANG))
!            STOP 'CHECK P12/P11'
       ENDIF
       IF(ABS(P34(IANG)/P11FIT(IANG))>1.0D0+1.0D-4)THEN
           WRITE(*,*)'P34(IANG)/P11FIT(IANG)=',P34(IANG)/P11FIT(IANG)
           P34(IANG)=sign(P11FIT(IANG),P34(IANG))
!           STOP 'CHECK P34/P11'
       ENDIF
    ENDDO
  ENDIF

  ANGCUT=COS(ANGLE_TRUNCATION/180.0D0*PI)

  IF(OUTPUTTST)THEN
    open(unit=21, file='fftest',status='replace')
    DO IANG=1,NUMANG
      WRITE(21,'(7(E13.5,2x))')ANG(IANG),P11FIT(IANG),P22(IANG),&
           P33(IANG),P44(IANG),P12(IANG),P34(IANG)
    ENDDO
    CLOSE(21)
  ENDIF

  BETAL_LOCAL=0.0D0
  call bfit(NUMLORD,NUMANG,ANG,P11FIT,ANGCUT,BETAL_LOCAL,FTRUNCFIT,DELTAM)

  IF(IMIE<NUMMIE+1) FTRUNC(IMIE)=FTRUNCFIT
  DO IANG=1,NUMANG
    MUF=COS(ANG(IANG)/180.0D0*PI)
! GET FITTED SCATTERING MATRIX FROM COEFFICIENTS
    CALL GETDMLlocal(MUF,NUMLORD,DL00,DL20,DL2P2,DL2N2)
    DO L=0,NUMLORD
       P11(IANG)=P11(IANG)+DL00(L)*BETAL_LOCAL(L)
    ENDDO
    P12(IANG)=P12(IANG)/P11FIT(IANG)*P11(IANG)
    P22(IANG)=P22(IANG)/P11FIT(IANG)*P11(IANG)
    P33(IANG)=P33(IANG)/P11FIT(IANG)*P11(IANG)
    P44(IANG)=P44(IANG)/P11FIT(IANG)*P11(IANG)
    P34(IANG)=P34(IANG)/P11FIT(IANG)*P11(IANG)
  ENDDO

! Do Not Change the following part within ***********

DO IANG=1,NUMANG
   RTMP=P22(IANG)
   P22(IANG)=P22(IANG)+P33(IANG)
   P33(IANG)=RTMP-P33(IANG)
ENDDO

ALPHAL_LOCAL=0.0D0
ZETAL_LOCAL=0.0D0
DELTAL_LOCAL=0.0D0
GAMMAL_LOCAL=0.0D0
EPSILONL_LOCAL=0.0D0
call bfit_ger(1,NUMLORD,NUMANG,ANG,P12,P11,ANGCUT,GAMMAL_LOCAL,DELTAM)
call bfit_ger(2,NUMLORD,NUMANG,ANG,P22,P11,ANGCUT,ALPHAL_LOCAL,DELTAM)
call bfit_ger(3,NUMLORD,NUMANG,ANG,P33,P11,ANGCUT,ZETAL_LOCAL,DELTAM)
call bfit_ger(4,NUMLORD,NUMANG,ANG,P44,P11,ANGCUT,DELTAL_LOCAL,DELTAM)
call bfit_ger(5,NUMLORD,NUMANG,ANG,P34,P11,ANGCUT,EPSILONL_LOCAL,DELTAM)

TMPL=ALPHAL_LOCAL
ALPHAL_LOCAL=(ALPHAL_LOCAL+ZETAL_LOCAL)/2.0D0
ZETAL_LOCAL=(TMPL-ZETAL_LOCAL)/2.0D0

! **************************

IF(OUTPUTTST) CALL PHF(NUMLORD,BETAL_LOCAL,ALPHAL_LOCAL,ZETAL_LOCAL,&
                       DELTAL_LOCAL,GAMMAL_LOCAL,EPSILONL_LOCAL)

IF(IMIE==NUMMIE+1 .AND. RAMAN_SOURCE_FLAG_LOCAL)THEN
  DO L=0,2
    WRAMANBETAL(L)=BETAL_LOCAL(L)        !AL1(IL)
    WRAMANALPHAL(L)=ALPHAL_LOCAL(L)       !AL2(IL)
    WRAMANZETAL(L)=ZETAL_LOCAL(L)        !AL3(IL)
    WRAMANDELTAL(L)=DELTAL_LOCAL(L)       !AL4(IL)
    WRAMANGAMMAL(L)=GAMMAL_LOCAL(L)       !BET1(IL)
    WRAMANEPSILONL(L)=-EPSILONL_LOCAL(L)  !BET2(IL)! convention sign change
                                            ! double check later
  ENDDO
ELSE
  DO L=0,MAXLORD
    BETAL(IMIE,L)=BETAL_LOCAL(L)        !AL1(IL)
    ALPHAL(IMIE,L)=ALPHAL_LOCAL(L)       !AL2(IL)
    ZETAL(IMIE,L)=ZETAL_LOCAL(L)        !AL3(IL)
    DELTAL(IMIE,L)=DELTAL_LOCAL(L)       !AL4(IL)
    GAMMAL(IMIE,L)=GAMMAL_LOCAL(L)       !BET1(IL)
    EPSILONL(IMIE,L)=-EPSILONL_LOCAL(L)  !BET2(IL)! convention sign change
                                            ! double check later
  ENDDO
ENDIF

IF(OUTPUTTST)THEN
  OPEN(UNIT=2,FILE='COEFF_FITTED')
  WRITE(2,*)FTRUNC(NUMMIE), NUMLORD
  WRITE(2,510)
  WRITE(2,590)
  DO L=0,NUMLORD
   WRITE(2,'(I5,6(F14.7,2X))')L,BETAL_LOCAL(L),ALPHAL_LOCAL(L), &
            ZETAL_LOCAL(L),DELTAL_LOCAL(L),GAMMAL_LOCAL(L),-EPSILONL_LOCAL(L)
  ENDDO
  close(2)
ENDIF     
DEALLOCATE(ANG,P11FIT,P11,P12,P22,P33,P34,P44)

ENDDO  ! NUMMIE DO
DEALLOCATE(BETAL_LOCAL,ALPHAL_LOCAL,ZETAL_LOCAL,DELTAL_LOCAL,&
           GAMMAL_LOCAL,EPSILONL_LOCAL,TMPL,DL00,DL20,DL2P2,DL2N2)
 509 FORMAT('Truncation factor,max l ord') 
 510 FORMAT('*********   EXPANSION COEFFICIENTS   *********') 
 590 FORMAT('    L     Beta(A1)        Alpha(A2)       ZETA(A3) ', &
     &'       Delta(A4)       Gamma(B1)       Epsilon(B2) ')   
END SUBROUTINE DELTAFIT


SUBROUTINE PHF(NUMLORD,BETAL,ALPHAL,ZETAL,DELTAL,GAMMAL,EPSILONL)
IMPLICIT NONE

INTEGER, INTENT(IN) ::NUMLORD
real*8,INTENT(IN),DIMENSION(0:*)::BETAL,ALPHAL,ZETAL,DELTAL,GAMMAL,EPSILONL
real*8,ALLOCATABLE,DIMENSION(:)::DL00,DL20,DL2P2,DL2N2

real*8 ::P11,P12,P22,P33,P34,P44
INTEGER :: IANG,L,NUMANG
real*8 :: PI,ANG,DANG,MU

PI=2.0D0*DASIN(1.0D0)

NUMANG=361

ALLOCATE(DL00(0:NUMLORD),DL20(0:NUMLORD),DL2P2(0:NUMLORD),DL2N2(0:NUMLORD))
  
DANG=180.0D0/DFLOAT(NUMANG-1)
ANG=-DANG

  OPEN(UNIT=19,FILE='PHASETEST',STATUS='REPLACE')
  DO IANG=1,NUMANG

    P11=0.0D0
    P12=0.0D0
    P22=0.0D0
    P33=0.0D0
    P34=0.0D0
    P44=0.0D0

    ANG=ANG+DANG
    MU=DCOS(ANG*PI/180.0D0)
    CALL GETDMLlocal(MU,NUMLORD,DL00,DL20,DL2P2,DL2N2)
    DO L=0,NUMLORD
      P11=P11+DL00(L)*BETAL(L)
      P12=P12+DL20(L)*GAMMAL(L)
      P22=P22+DL2P2(L)*ALPHAL(L)+DL2N2(L)*ZETAL(L)
      P33=P33+DL2N2(L)*ALPHAL(L)+DL2P2(L)*ZETAL(L)
      P34=P34+DL20(L)*EPSILONL(L)
      P44=P44+DL00(L)*DELTAL(L)
    ENDDO

    WRITE(19,'(7(E13.5,2x))')ANG,P11,P22,P33,P44,P12,P34
    IF(P11<0.0D0) THEN
      WRITE(*,*)'!!!!!!!!!!!!!'
      WRITE(*,*)'WARNING, NEGATIVE TRUNACATED PHASE FUNCTION'
      WRITE(*,*)'INCREASE MAXLORD TO RETRY'
      WRITE(*,*)'!!!!!!!!!!!!!'
    ENDIF
  ENDDO

  CLOSE(19)
DEALLOCATE(DL00,DL20,DL2P2,DL2N2)
END SUBROUTINE PHF

      SUBROUTINE dgauleg(x1,x2,x,w,n)
      INTEGER n
      real*8 x1,x2,x(n),w(n)
      DOUBLE PRECISION EPS
      PARAMETER (EPS=3.d-14)

      INTEGER i,j,m
      DOUBLE PRECISION p1,p2,p3,pp,xl,xm,z,z1
	  real*8 fi,fj,fn
      fn=dfloat(n)
	  
      m=(n+1)/2
      xm=0.5d0*(x2+x1)
      xl=0.5d0*(x2-x1)
      do 12 i=1,m
	    fi=dfloat(i)
        z=cos(3.14159265358979323846d0*(fi-.25d0)/(fn+.5d0))
1       continue
          p1=1.d0
          p2=0.d0
          do 11 j=1,n
		    fj=dfloat(j)
            p3=p2
            p2=p1
            p1=((2.d0*fj-1.d0)*z*p2-(fj-1.d0)*p3)/fj
11        continue
          pp=n*(z*p1-p2)/(z*z-1.d0)
          z1=z
          z=z1-p1/pp
        if(abs(z-z1).gt.EPS)goto 1
        x(i)=xm-xl*z
        x(n+1-i)=xm+xl*z
        w(i)=2.d0*xl/((1.d0-z*z)*pp*pp)
        w(n+1-i)=w(i)
12    continue
      return
      END


SUBROUTINE ANGASSIGN(NUMANG,ANG)
INTEGER :: NUMANG,IANG
REAL*8,DIMENSION(NUMANG)::ANG
REAL*8 :: RTMP
ANG=0.0D0
RTMP=0.001D0
ANG(1)=0.0D0
DO IANG=2,11
ANG(IANG)=ANG(IANG-1)+RTMP
ENDDO
RTMP=0.01D0
DO IANG=12,20
ANG(IANG)=ANG(IANG-1)+RTMP
ENDDO
RTMP=0.1D0
DO IANG=21,29
ANG(IANG)=ANG(IANG-1)+RTMP
ENDDO
RTMP=1.0D0
DO IANG=30,38
ANG(IANG)=ANG(IANG-1)+RTMP
ENDDO

RTMP=5.0D0
DO IANG=39,72
ANG(IANG)=ANG(IANG-1)+RTMP
ENDDO

END SUBROUTINE ANGASSIGN
